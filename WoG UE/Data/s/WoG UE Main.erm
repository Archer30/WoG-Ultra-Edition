ZVSE
ERMS_ScriptDate=24.8(August).2004
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2004.6.29.918

** Данный скрипт является краеугольным камнем WoG UE
** Опции:

** 1) Установка характеристик новых существ, а также изменения баланса

** 2) Перенастройка параметров героев

** 3) Кузница

** 4) Функции новых артефактов

** 5) Боевые механики
---------------------------------------------------------------------------------------------------------------------------------

*?FU(WoG_UE.GlobalSetup); Корректировка параметров при входе в игру
!?PI;
*Паладин
!!MA:O200/0 C200/6/500 A200/14 D200/12 P200/45 M200/8 E200/12 S200/7 L200/3 X200/32784 I200/690 F200/690 V200/9 H200/17 G200/4;
*Фанатик войны
!!MA:O169/0 L169/4 I169/770 F169/770 V169/6 H169/12 G169/3 S169/7 P169/40 E169/14;
*Чемпион войны
!!MA:O201/0 C201/6/1650 A201/18 D201/18 P201/120 M201/20 E201/30 S201/12 L201/5 X201/17 I201/2690 F201/2290 V201/4 H201/10 G201/2;
*Верховный Архангел
!!MA:C150/5/8 C150/6/14000 M150/60 E150/60 I150/18000 F150/13000;
*Серафим [гипер]
!!MA:U150/202 O202/0 C202/6/64000 C202/5/28 A202/55 D202/45 P202/1200 M202/85 E202/85 S202/25 L202/6 X202/65811 I202/62000 F202/60000 V202/1 H202/3 G202/1;

*Сильванский кентавр
!!MA:O192/1 C192/6/110 A192/6 D192/4 P192/10 M192/2 E192/3 L192/1 N192/16 I192/160 F192/160 V192/18 H192/30 G192/14;
*Гном-лепрекон
!!MA:O203/1 C203/6/210 A203/7 D203/7 P203/25 M203/4 E203/6 S203/6 L203/2 X203/589840 I203/230 F203/240 V203/14 H203/25 G203/8;
*Эльф-Сатир
!!MA:O204/1 C204/6/325 A204/9 D204/9 P204/25 M204/4 E204/7 S204/7 L204/3 X204/98320 I204/440 F204/440 V204/12 H204/20 G204/7;
*Эльф-Фавн [гипер]
!!MA:O207/1 C207/6/410 A207/10 D207/10 P207/30 M207/5 E207/7 S207/8 L207/3 X207/98320 I207/500 F207/500 V207/12 H207/17 G207/7;
*Алмазный дракон
!!MA:C151/4/7 C151/6/12500 I151/16500 F151/12200;

*Санта-гремлин
!!MA:O173/2 C173/6/110 L173/1 I173/150 F173/150;
*Химера
!!MA:O208/2 C208/6/200 A208/8 D208/8 P208/20 M208/4 E208/5 S208/9 L208/1 X208/2 I208/230 F208/200 V208/14 H208/25 G208/9;
*Золотой голем
!!MA:O116/2 C116/6/430 A116/10 P116/40 M116/5 E116/8 S116/6 L116/3 I116/430 F116/430 V116/13 H116/21 G116/6;
*Алмазный голем [гипер]
!!MA:O117/2 C117/6/480 A117/12 P117/50 M117/6 E117/10 S117/6 L117/3 I117/470 F117/470 V117/12 H117/18 G117/6;
*Громовержец
!!MA:C152/5/7 C152/6/13500 M152/45 E152/70 I152/17500 F152/12500;

*Суккуб
!!MA:O186/3 C186/6/100 A186/5 D186/5 P186/6 M186/2 E186/3 S186/7 L186/0 X186/18 I186/120 F186/120 V186/20 H186/30 G186/15;
*Кошмар
!!MA:O214/3 C214/6/500 A214/12 D214/12 P214/50 M214/8 E214/10 S214/7 L214/4 X214/1049 I214/640 F214/640 V214/8 H214/16 G214/4;
*Адский конь [гипер]
!!MA:O216/3 C216/6/600 A216/14 D216/12 P216/60 M216/9 E216/11 L216/4 X216/17433 I216/770 F216/770 V216/8 H216/15 G216/4;
*Ифрит-Раджа
!!MA:O218/3 C218/6/1600 A218/19 D218/17 P218/110 M218/20 E218/26 S218/14 L218/6 X218/86038 I218/2900 F218/2450 V218/4 H218/10 G218/2;
*Антихрист
!!MA:C153/1/6 C153/6/12250 P153/450 I153/15500 F153/12000;

*Механизированный зомби
!!MA:O224/4 C224/6/200 A224/7 D224/7 P224/25 M224/4 E224/5 S224/5 L224/1 X224/393216 I224/200 F224/220 V224/15 H224/25 G224/8;
*Привидение
!!MA:O159/4 C159/6/380 A159/8 D159/8 P159/20 M159/4 E159/6 S159/7 L159/2 X159/393218 I159/350 F159/300 V159/10 H159/20 G159/7;
*Жнец
!!MA:O223/4 C223/6/1900 A223/20 D223/18 P223/130 M223/20 E223/35 S223/9 L223/5 X223/393217 I223/2700 F223/2300 V223/4 H223/10 G223/2;
*Кровавый Дракон
!!MA:C154/1/6 C154/6/11500 A154/27 D154/25 I154/15500 F154/12000 S154/16;
*Дракон-Лич [гипер]
!!MA:U154/196 N196/8 C196/6/47500 C196/1/26 A196/50 D196/40 M196/50 O196/4 I196/50000 F196/45000;

*Троглодит-изверг
!!MA:O225/5 C225/6/100 A225/5 D225/5 P225/7 M225/2 E225/4 S225/5 L225/0 X225/16 I225/120 F225/120 V225/20 H225/30 G225/14;
*Смертельный глаз
!!MA:O226/5 C226/6/410 A226/11 D226/9 P226/28 M226/5 E226/8 S226/9 L226/2 I226/450 F226/450 V226/10 H226/20 G226/7;
*Легендарный минотавр
!!MA:O229/5 C229/6/650 A229/14 D229/14 P229/65 M229/15 E229/20 S229/8 L229/5 X229/16 I229/1000 F229/1000 V229/6 H229/12 G229/3;
*Проклятый минотавр [гипер]
!!MA:O230/5 C230/6/825 A230/15 D230/15 P230/70 M230/16 E230/22 S230/9 L230/5 X230/16 I230/1200 F230/1200 V230/5 H230/10 G230/3;
*Темный Дракон
!!MA:U155/231 C155/3/7 C155/6/13000 S155/16 A155/35 D155/35 I155/16800 F155/12400;

*Орк-гладиатор
!!MA:O234/6 C234/6/345 A234/12 D234/7 P234/35 M234/5 E234/8 S234/7 L234/2 X234/16 I234/420 F234/420 V234/12 H234/20 G234/7;
*Повелитель огров
!!MA:O235/6 C235/6/650 A235/14 D235/8 P235/60 M235/6 E235/12 S235/6 L235/4 N235/6 X235/4116 I235/810 F235/810 V235/8 H235/16 G235/4;
*Громовой птероящер
!!MA:O236/6 C236/6/650 A236/14 D236/12 P236/65 M236/12 E236/16 S236/12 L236/5 X236/19 I236/1000 F236/1000 V236/6 H236/12 G236/3;
*Громовой дракон [гипер]
!!MA:O237/6 C237/6/800 A237/15 D237/12 P237/70 M237/14 E237/18 S237/12 L237/5 X237/19 I237/1200 F237/1100 V237/5 H237/10 G237/3;
*Чудище-привидение
!!MA:C156/4/7 C156/6/12500 A156/30 D156/30 E156/70 S156/10 I156/17400 F156/12500;

*Оборотень
!!MA:O239/7 C239/6/100 A239/5 D239/7 P239/8 M239/3 E239/4 S239/6 L239/1 I239/130 F239/130 V239/17 H239/30 G239/14;
*Ящер-гвардеец
!!MA:O241/7 C241/6/230 A241/8 D241/8 P241/20 M241/4 E241/7 S241/7 L241/2 X241/17 I241/280 F241/220 V241/16 H241/25 G241/9;
*Ящер-лейтенант [гипер]
!!MA:O242/7 C242/6/280 A242/8 D242/10 P242/20 M242/5 E242/7 S242/7 L242/2 X242/17 I242/310 F242/280 V242/14 H242/23 G242/9;
*Горыныч
!!MA:O168/7 C168/6/2000 A168/17 D168/17 P168/140 M168/22 E168/28 S168/11 L168/6 I168/3110 F168/2500 V168/4 H168/10 G168/2;
*Адская Гидра
!!MA:C157/3/6 C157/6/12000 A157/28 D157/30 I157/17500 F157/12500;

*Волшебница
!!MA:O246/8 C246/6/110 A246/4 D246/4 P246/4 M246/2 E246/3 S246/9 L246/0 X246/65554 N246/16 I246/130 F246/130 V246/16 H246/30 G246/16;
*Морская нимфа
!!MA:O248/8 C248/6/375 A248/9 D248/10 P248/35 M248/5 E248/8 S248/9 L248/3 X248/131074 I248/415 F248/415 V248/12 H248/20 G248/6;
*Дух Океана [гипер]
!!MA:O250/8 C250/6/435 A250/10 D250/10 P250/40 M250/5 E250/8 S250/10 L250/3 X250/131074 I250/430 F250/430 V250/12 H250/17 G250/6;
*Астральный элементаль
!!MA:O251/8 C251/6/1400 A251/17 D251/17 P251/100 M251/20 E251/25 S251/9 L251/5 X251/720896 I251/2500 F251/2200 V251/4 H251/10 G251/2;
*Дух Погребального Костра
!!MA:C158/1/7 C158/6/12500 A158/30 D158/30 P158/500 I158/16000 F158/12000;

*Глубинный змей
!!MA:O122/-1 C122/6/3250 A122/30 D122/20 P122/225 M122/35 E122/55 S122/7 L122/6 X122/17 I122/6000 F122/5300 V122/3 H122/7 G122/1;
*Паук-падальщик
!!MA:O124/-1 C124/6/50 A124/3 D124/4 P124/7 M124/2 E124/5 S124/5 L124/0 X124/17 I124/85 F124/75 V124/15 H124/30 G124/15;
*Оса-падальщик
!!MA:O126/-1 C126/6/50 A126/3 D126/3 P126/5 M126/3 E126/5 S126/7 L126/0 X126/19 I126/85 F126/75 V126/15 H126/30 G126/15;
*Подземный пожиратель
!!MA:O128/-1 C128/6/350 A128/10 D128/16 P128/50 M128/8 E128/12 S128/7 L128/4 X128/17 I128/640 F128/640 V128/10 H128/16 G128/4;

*Правки характеристик
!!MA:N14/4 X14/4117; [Кентавры]
!!MA:N15/6 X15/4117; [Капитаны Кентавров]

!!MA:A1/5;
!!MA:D2/4;
!!MA:D3/5;
!!MA:C5/6/275;
!!MA:C8/6/430 D8/8;
!!MA:C9/6/480 E9/14;
!!MA:M12/40 E12/40;
!!MA:D15/4;
!!MA:D18/6;
!!MA:F18/205 I18/244;
!!MA:P19/20 C19/6/280 D19/7 F19/225 I19/361;
!!MA:C20/6/300 M20/6 E20/10;
!!MA:P21/35 C21/6/350 M21/6 E21/10 F21/440 I21/555;
!!MA:C22/6/425;
!!MA:C23/6/480 A23/10;
!!MA:C25/6/1000;
!!MA:F27/5700 I27/7500;
!!MA:E29/3;
!!MA:E31/4;
!!MA:C32/6/180;
!!MA:C33/6/300 E33/6;
!!MA:C39/6/1400;
!!MA:C36/6/500;
!!MA:E40/50;
!!MA:E41/50 C41/6/4500 F41/5500;
!!MA:C42/6/40;
!!MA:C43/6/50;
!!MA:D45/5;
!!MA:C48/6/320;
!!MA:C49/6/380 A49/11 D49/11;
!!MA:C50/6/500 I50/700;
!!MA:C51/6/600 I51/850;
!!MA:C54/6/2500;
!!MA:C55/6/3750 P55/220 A55/23 D55/25;
!!MA:C56/6/50;
!!MA:C57/6/60;
!!MA:A59/6 D59/6;
!!MA:P61/20 C61/6/250;
!!MA:D63/12 F63/700;
!!MA:C64/6/450;
!!MA:C65/6/550 M65/12;
!!MA:C67/6/1400;
!!MA:I68/3100;
!!MA:C70/6/40;
!!MA:C71/6/50;
!!MA:M73/2 E73/5;
!!MA:C77/6/370;
!!MA:A78/12 D78/12;
!!MA:A79/14 D79/12;
!!MA:C81/6/1000;
!!MA:S83/16;
!!MA:G84/16;
!!MA:E85/3 G85/16;
!!MA:G86/10;
!!MA:G87/10;
!!MA:C88/6/165;
!!MA:C89/6/180 D89/5;
!!MA:C90/6/400 M90/5 E90/9;
!!MA:C91/6/500 M91/5 E91/9;
!!MA:M92/9 E92/13 C92/6/500;
!!MA:C93/6/600;
!!MA:C95/6/1000 D95/14;
!!MA:C96/6/2000 A96/18 D96/18;
!!MA:C97/6/3500 C97/4/2 A97/24 D97/24;
!!MA:C98/6/40 S98/5 G98/14;
!!MA:C99/6/50 S99/6 G99/14;
!!MA:C101/6/150;
!!MA:C103/6/650;
!!MA:C104/6/200;
!!MA:C105/6/230 M105/3 E105/6;
!!MA:C108/6/800;
!!MA:A109/15 D109/15;
!!MA:A112/7 D112/7 C112/6/180 E112/5 F112/210 I112/210 G112/8;
!!MA:S113/5;
!!MA:C115/6/250;
!!MA:C118/6/30 G118/16;
!!MA:C119/6/45 A119/3 G119/16;
!!MA:C121/6/900 D121/15;
!!MA:C123/6/300;
!!MA:C125/6/450 P125/45;
!!MA:A127/8 D127/8 C127/6/230 E127/5 S127/7 F127/246 I127/268 G127/8;
!!MA:M129/5 E129/8;
!!MA:C130/6/2000 A131/20 D131/20 G130/1;
!!MA:C131/6/4000 C131/1/2 A131/25 D131/25 G131/1 P131/300 M131/35 E131/50 F131/6800 I131/8750 V131/3 H131/8;
!!MA:I132/60000;
!!MA:M134/25 E134/40;
!!MA:C135/6/14000;
!!MA:G136/3;
!!MA:G137/5;
!!MA:A141/8 D141/8;
!!MA:G144/4 F144/850 I144/850;
!!MA:M146/9 E146/14;

*Сказочные драконы колдуют Волшебную Стрелу вместо Цепной Молнии
*!UN:C6535288/4/15;

*Отменить у оборотней берсерк в полнолуние
; © MoP
**UN:C7762830/4/12345;
**UN:C7762908/4/12345;
**UN:C7763122/4/12345;

; Нашел радиусы исследования территорий, может, пригодятся когда-нибудь)
**UN:C6547928/4/5; Радиус без навыка разведки
**UN:C6547932/4/6; С базовой разведкой
**UN:C6547936/4/7; С продвинутой разведкой
**UN:C6547940/4/8; С экспертной разведкой

!!UN:V?y1/?y2; Проверяем версию Эры
!!FU&y2>3306:E; Выход, если подходит
!!IF:M^Мод WoG Ultra Edition был установлен неправильно и не может работать корректно.^;
!!IF:M^Пожалуйста, скачайте версию 2.5.0 по официальной ссылке от разработчика (с форума или из группы ВК - Герои 3: Клуб мододела), а затем установите любую более позднюю версию поверх нее.
Таким образом Вы гарантированно избежите всех проблем. Удачи и приятной игры!^;


!#FU(WoG_UE.GlobalSetup):P; Корректировка параметров до первого дня


*Правки героев
!#HE63:X3/21;
!#HE57:X3/22;
!#UN:G2/63/3/63;
!#UN:G2/57/3/57;

!#HE77:X3/54;
!#HE79:X8/1; Нагаш
!#HE91:X3/38;
!#VRz939:Sz125190;
!#UN:G2/91/1/91;
!#UN:G2/91/2/939;
!#UN:G2/91/3/91;
!#HE95:X3/19;
!#VRz938:Sz125191;
!#UN:G2/79/2/938;

!#HE28:X2/5;
!#HE40:X4/0/0/0/0;
!#UN:G2/40/3/145;
!#HE110:X4/0/0/0/0;
!#HE121:X4/0/0/0/0;

!#UN:G2/121/3/289;
!#UN:G2/110/3/289;
!#HE139:X3/46;
!#UN:G2/139/3/124;
!#HE124:X3/46;
!#UN:G2/124/3/124;

*Орис и Розик дают дерево и руду
!?FU(OnEveryDay);
!!VRn:Sc;
!!FU&n=1:E; Не даем ресурсы в первый день
!!OW:C?y1; Получаем цвет текущего игрока
!!HE121:O?y2; Цвет владельца Розика
!!if&y1=y2:; Если принадлежит текущему игроку
 !!OW:Ry1/0/d1; Добавлем дерево
 !!OW:Ry1/2/d1; Добавляем руду
!!en:;
!!HE110:O?y3; Цвет владельца Орис
!!if&y1=y3:; Если принадлежит текущему игроку
 !!OW:Ry1/0/d1; Добавлем дерево
 !!OW:Ry1/2/d1; Добавляем руду
!!en:;
---------------------------------------------------------------------------------------------------------------------------------

*Кузница
!#VRv6769:C1;

!?CM1&999;
!!FU&v6769=0:E;                Выход, если функция отключена
!!CM:F?y1;
!!CM:I?y2;
!!FU&y2<>16:E;                 Выход, если нажали не на кузницу
!!CA-1:H1/?y3;
!!FU&y3=-1:E;                  Выход, если нет героя
!!FU(FORGE_NormalClick)&y1=32:Py3;
!!VRy25:Sc;
!!FU&y25<22:E;                 Выход, если дата не подходит
!!FU(FORGE_ClickOnBuilding)&y1<>32:Py3;

!?FU(FORGE_NormalClick);
!!CM:R0;
!!CA-1:O?y4;
!!OW:C?y5;
!!CA-1:T?y6;

!!VRy25:Sc;
!!IF&y25<22:M1/125228;
!!FU&y25<22:E;

!!FU15037&y4=y5:Px1/y6/1/1;

!?FU(FORGE_ClickOnBuilding);
!!HEx1:E?j/?y4/1;
!!FU&y4<5:E;                   Выход, если слишком низкий уровень
!!OW:C?y5;
!!CA-1:T?y6;
!!VRv1:S0;
!!HEx1&y6=0:A2/171/?v1/?y7;
!!HEx1&y6=1:A2/175/?v1/?y7;
!!HEx1&y6=2:A2/176/?v1/?y7;
!!HEx1&y6=3:A2/177/?v1/?y7;
!!HEx1&y6=4:A2/173/?v1/?y7;
!!HEx1&y6=5:A2/172/?v1/?y7;
!!HEx1&y6=6:A2/106/?v1/?y7;
!!HEx1&y6=7:A2/174/?v1/?y7;
!!HEx1&y6=8:A2/178/?v1/?y7;
!!FU&v1>0:E;                   Выход, если у героя есть артефакт
!!CA-1:O?y8;
!!OW:R-1/6/>4999;
!!FU&-1:E;                     Выход, если не хватает ресурсов
!!OW:R-1/0/>9;
!!FU&-1:E;
!!OW:R-1/2/>9;
!!FU&-1:E;
!!OW&y6=5|y6=7:R-1/1/>4;
!!FU&-1:E;
!!OW&y6=0|y6=3:R-1/3/>4;
!!OW&y6=4:R-1/3/>4;
!!FU&-1:E;
!!OW&y6=1|y6=2:R-1/4/>4;
!!FU&-1:E;
!!OW&y6=6|y6=8:R-1/5/>4;
!!FU&-1:E;
!!FU15037&y8=y5:Px1/y6/1/1; //x3 - человек в замке

*Запуск функций при входе в город
!?OB98;
!!FU&v6769=0|-1000:E;
!!HE-1:P?y1/?y2/?y4;
!!OBy1/y2/y4:T?n;
!!FU&n<>98:E;
!!CAy1/y2/y4:B3/16;
!!FU&-1:E;
!!HE-1:E?j/?l/1;
!!FU&l<5:E;

!!CAy1/y2/y4:T?y6;
!!VRv1:S0;
!!HE-1&y6=0:A2/171/?v1/?y7;
!!HE-1&y6=1:A2/175/?v1/?y7;
!!HE-1&y6=2:A2/176/?v1/?y7;
!!HE-1&y6=3:A2/177/?v1/?y7;
!!HE-1&y6=4:A2/173/?v1/?y7;
!!HE-1&y6=5:A2/172/?v1/?y7;
!!HE-1&y6=6:A2/106/?v1/?y7;
!!HE-1&y6=7:A2/174/?v1/?y7;
!!HE-1&y6=8:A2/178/?v1/?y7;
!!FU&v1>0:E;

!!HE-1:N?y3;
!!CAy1/y2/y4:O?y4;
!!OW:C?y5;

!!OW:R-1/6/>4999;
!!FU&-1:E;                     Выход, если не хватает ресурсов
!!OW:R-1/0/>9;
!!FU&-1:E;
!!OW:R-1/2/>9;
!!FU&-1:E;
!!OW&y6=5|y6=7:R-1/1/>4;
!!FU&-1:E;
!!OW&y6=0|y6=3:R-1/3/>4;
!!OW&y6=4:R-1/3/>4;
!!FU&-1:E;
!!OW&y6=1|y6=2:R-1/4/>4;
!!FU&-1:E;
!!OW&y6=6|y6=8:R-1/5/>4;
!!FU&-1:E;

!!VRy25:Sc;

!!FU15037&y4=y5/y25>21:Py3/y6/1/0;//x3 - человек в замке

!?FU15037;
!!HEx1:E?j/?v9/1;
!!IF&v9<5/x3=1:M1/z125206;
!!FU&v9<5/x3=1:E;
!!FU&v9<5/-1000/x3<>2:E;

!!IF&1000:Q1/z125207;
!!FU&-1/1000:E;
!!VRy81&x2=0:S171;
!!VRy81&x2=1:S175;
!!VRy81&x2=2:S176;
!!VRy81&x2=3:S177;
!!VRy81&x2=4:S173;
!!VRy81&x2=5:S172;
!!VRy81&x2=6:S106;
!!VRy81&x2=7:S174;
!!VRy81&x2=8:S178;
!!VRy82:S125208 +x2;
!!IF&1000:Q2/8/y81/2/zy82;
!!FU&-2/1000:E;

!!IF:V1/1;              Ставим флаг на истину

!!OW:C?i;               Текущий игрок
!!OW:Ri/0/?v3;          Дерево
!!OW:Ri/2/?v4;          Руда
!!OW:Ri/1/?v5;          Ртуть
!!OW:Ri/3/?v6;          Сера
!!OW:Ri/4/?v7;          Кристаллы
!!OW:Ri/5/?v8;          Драг. Камни
!!OW:Ri/6/?v9;          Золото

!!IF&v9<5000:V1/0;      Обнуляем флаг, если не хватает золота
!!IF&v3<10:V1/0;        Eсли не хватает дерева
!!IF&v4<10:V1/0;        Eсли не хватает руды

!!IF&v6<5/x2=0:V1/0;    Если не хватает других ресурсов
!!IF&v7<5/x2=1:V1/0;
!!IF&v7<5/x2=2:V1/0;
!!IF&v6<5/x2=3:V1/0;
!!IF&v6<5/x2=4:V1/0;
!!IF&v5<5/x2=5:V1/0;
!!IF&v8<5/x2=6:V1/0;
!!IF&v5<5/x2=7:V1/0;
!!IF&v8<5/x2=8:V1/0;

!!IF&-1/1000:M1/z125204;
!!FU&-1/1000:E;

!!OW&x2=0/1000:Ri/3/d-5;Забираем
!!OW&x2=1/1000:Ri/4/d-5;
!!OW&x2=2/1000:Ri/4/d-5;
!!OW&x2=3/1000:Ri/3/d-5;
!!OW&x2=4/1000:Ri/3/d-5;
!!OW&x2=5/1000:Ri/1/d-5;
!!OW&x2=6/1000:Ri/5/d-5;
!!OW&x2=7/1000:Ri/1/d-5;
!!OW&x2=8/1000:Ri/5/d-5;

!!OW&1000:Ri/6/d-5000;
!!OW&1000:Ri/0/d-10;
!!OW&1000:Ri/2/d-10;

!!IF&1000:M1/z125205;
!!HEx1:A4/y81;          Если все хорошо - даем артефакт
!!UN&x4=1:R4;

!?FU15046;              Проверяем ИИ
!!VRv10:S0;
!!HE-1&x1=0:A2/171/?v10/?y7;
!!HE-1&x1=1:A2/175/?v10/?y7;
!!HE-1&x1=2:A2/176/?v10/?y7;
!!HE-1&x1=3:A2/177/?v10/?y7;
!!HE-1&x1=4:A2/173/?v10/?y7;
!!HE-1&x1=5:A2/172/?v10/?y7;
!!HE-1&x1=6:A2/106/?v10/?y7;
!!HE-1&x1=7:A2/174/?v10/?y7;
!!HE-1&x1=8:A2/178/?v10/?y7;

*Объект на карте
!?OB145;
!!VRz-10:S^STORE.WAV^;
!!SN&1000:Pz-10;
!!VRy1:S0;
!!VRy2:S0;
!!VRy3:S0;
!!VRy4:S0;

!!HE-1:N?y1;
!!HE-1:P?y2/?y3/?y4;
!!VRy5:Sy2;
!!VRy5:+y3;
!!VRy5:+y4;
!!VRy5:%9;

!!FU15046&-1000:Py5;
!!FU&-1000/v10>0:E;     Если у ИИ уже есть артефакты - выход

!!FU15037:Py1/y5/2;

!?OB98&-1000;
!!FU&v6769=0:E;
!!VRy25:Sc;
!!FU&y25<22:E;
!!HE-1:N?v1;
!!HE-1:P?v3/?v4/?v5;
!!VRv4:-1;
!!HE-1:O?y1;
!!CAv3/v4/v5:O?y2;
!!FU&y1<>y2:E;

!!CAv3/v4/v5:T?v2;
!!FU15046:Pv2;
!!FU&-1000/v10>0:E;     Если у ИИ уже есть артефакты - выход

!!FU15037:Pv1/v2/0/0;

!$OB98&-1000;
!!FU&v6769=0:E;
!!HE-1:O?y1;
!!FU&y1=-1:E;
!!VRy25:Sc;
!!FU&y25<22:E;
!!HE-1:N?v1;
!!HE-1:P?v3/?v4/?v5;
!!VRv4:-1;

!!CAv3/v4/v5:T?v2;
!!FU15046:Pv2;
!!FU&-1000/v10>0:E;     Если у ИИ уже есть артефакты - выход

!!FU15037:Pv1/v2/0/0;
!!FU15057:P-1;

*Удаление лишних копий
!?FU15057;
!!HEx1:A2/106/?v1/?n;
!!VRv1&v1>1:-1;
!!HEx1&v1>1:A3/106/v1/0;Брелок
!!HEx1:A2/173/?v1/?n;
!!VRv1&v1>1:-1;
!!HEx1&v1>1:A3/173/v1/0;Шлем
!!HEx1:A2/171/?v1/?n;
!!VRv1&v1>1:-1;
!!HEx1&v1>1:A3/171/v1/0;Ятаган
!!HEx1:A2/172/?v1/?n;
!!VRv1&v1>1:-1;
!!HEx1&v1>1:A3/172/v1/0;Щит
!!HEx1:A2/178/?v1/?n;
!!VRv1&v1>1:-1;
!!HEx1&v1>1:A3/178/v1/0;Кольцо
!!HEx1:A2/174/?v1/?n;
!!VRv1&v1>1:-1;
!!HEx1&v1>1:A3/174/v1/0;Кольчуга
!!HEx1:A2/176/?v1/?n;
!!VRv1&v1>1:-1;
!!HEx1&v1>1:A3/176/v1/0;Мантия
!!HEx1:A2/177/?v1/?n;
!!VRv1&v1>1:-1;
!!HEx1&v1>1:A3/177/v1/0;Сапоги
!!HEx1:A2/175/?v1/?n;
!!VRv1&v1>1:-1;
!!HEx1&v1>1:A3/175/v1/0;Рог

!?BA0;                  Перед каждым боем у героев ИИ удаляются лишние копии
!!BA:H0/?y1;
!!BA:H1/?y2;
!!FU&y2=-2:E;
!!HEy1:O?y3;
!!HEy2:O?y4;
!!OW:Iy3/?y5;
!!OW:Iy4/?y6;
!!FU15057&y5=1:Py1;
!!FU15057&y6=1:Py2;
---------------------------------------------------------------------------------------------------------------------------------

** Артефакты

*Установка запретов
!#UN:A12/1;             Гладиус Титана
!#UN:A18/1;             Щит Часового
!#UN:A24/1;             Шлем Небесного Грома
!#UN:A30/1;             Латы Титана
!#UN:A34/1;             Щит Львиной Храбрости
!#UN:A35/1;             Меч правосудия
!#UN:A36/1;             Шлем Божественного Просвещения
!#UN:A56/1;             Сапоги Мертвеца
!#UN:A72/1;             Крылья Ангела
!#UN:A128/1;            Клинок Армагеддона
!#UN:A129/1;            Альянс Ангелов
!#UN:A130/1;            Плащ Короля Нежити
!#UN:A131/1;            Эликсир Жизни
!#UN:A132/1;            Доспехи Проклятого
!#UN:A133/1;            Статуя Легиона
!#UN:A134/1;            Мощь Отца Драконов
!#UN:A135/1;            Грохот Титана
!#UN:A136/1;            Шляпа Адмирала
!#UN:A137/1;            Лук Снайпера
!#UN:A138/1;            Колодец Волшебника
!#UN:A140/1;            Рог изобилия
!#UN:A156/1;            Знамя полководца
!#UN:A157/1;            Багровый щит возмездия
!#UN:P234/1;            Отмечаем опцию
!#UN:A161/1;            Пустые артефакты
!#UN:A162/1;
!#UN:A163/1;
!#UN:A164/1;
!#UN:A165/1;
!#UN:A166/1;
!#UN:A167/1;
!#UN:A168/1;
!#UN:A169/1;
!#UN:A170/1;

!#UN:A179/1;           Запрет на появление требушета
!#UN:A179/2/10;        Требушет одевается в слот баллисты


*Багровый щит возмездия
!#UN:P236/1;
!#UN:A13/157/9/15/21/27;Настраиваем компоненты
!?AE1;
!!FU&v998<>157:E;
!!HE-1:Fd2/d2/d0/d0;    Увеличиваем атаку и защиту

!?AE0;
!!FU&v998<>157:E;
!!HE-1:Fd-2/d-2/d0/d0;  Уменьшаем атаку и защиту

*Брелок Отрицательности
!#UN:A106/3/8;
!#UN:A106/1/7000;
!?AE1;
!!FU&v998<>106:E;
!!HE-1:Fd0/d1/d1/d0;    Увеличиваем силу и защиту

!?AE0;
!!FU&v998<>106:E;
!!HE-1:Fd0/d-1/d-1/d0;  Уменьшаем силу и защиту

!#UN:P226/1; [Gold Tower Arrow]
!#UN:A142/1;

!#UN:P159/1; [Tome of Air Magic]
!#UN:A87/1;

!#UN:P160/1; [Tome of Earth Magic]
!#UN:A89/1;

!#UN:P161/1; [Tome of Fire Magic]
!#UN:A86/1;

!#UN:P162/1; [Tome of Water Magic]
!#UN:A88/1;

!#UN:P163/1; [Spellbinder's Hat]
!#UN:A124/1;

!#UN:C5138099/1/2; Кольцо жизненной силы
!#UN:C5138135/1/4; Склянка Жизненной Силы

*Брелок ясновидения
!#UN:A101/3/4;          Теперь малый артефакт

*Символ знаний
!#UN:A65/3/2;           Теперь артефакт-сокровище

*Крылья ангела
!#UN:A72/1/30000;       Повышаем цену

*Оковы Войны (в бою с нейтралами можно убежать с поля боя) by igrik

!?BG0; [перед действием в битве]
!!BG:A?y1; [узнать тип действия]

!!FU&y1<>4:E;  [выход, если не побег]

!!BA:H1/?y3; [узнать героя защитника]

!!UN&y3=-2:C4689325/1/2; [подменить на грааль (а он в рюкзаке)]

!?BA53; [после битвы]
!!UN:C4689325/1/125;


********************************************************************************
; Новые артефакты


*Золотой Ятаган
!#UN:A171/1;
!#UN:A171/3/8;
!#UN:A171/1/7000;

*Зачарованный щит
!#UN:A172/1;
!#UN:A172/3/8;
!#UN:A172/1/7000;

*Стальной Шлем
!#UN:A173/1;
!#UN:A173/3/8;
!#UN:A173/1/7000;

*Подавляющая Кольчуга
!#UN:A174/1;
!#UN:A174/3/8;
!#UN:A174/1/7000;

*Боевой Рог
!#UN:A175/1;
!#UN:A175/3/8;
!#UN:A175/1/7000;

*Магическая Мантия
!#UN:A176/1;
!#UN:A176/3/8;
!#UN:A176/1/7000;

*Подкованные Сапоги
!#UN:A177/1;
!#UN:A177/3/8;
!#UN:A177/1/7000;

*Кольцо Скорости
!#UN:A178/1;
!#UN:A178/3/8;
!#UN:A178/1/7000;

!?AE1;
!!FU&v998<>178:E;
!!HE-1:A2/178/?y99/?v1;
!!HE-1&v1<>1:Fd0/d0/d2/d2; Увеличиваем силу и знание, если еще не надето

!?AE0;
!!FU&v998<>178:E;
!!HE-1:A2/178/?y99/?v1; Проверяем, надето ли кольцо
!!HE-1&v1=1:Fd0/d0/d-2/d-2; Уменьшаем силу и знание, если надето лишь единожды
********************************************************************************
; Дополнительные свойства новых артефактов


; Зачарованный щит и Стальной шлем
!?BG0&v997>-1;
!!BG:Q?y1;
!!BA:Hy1/?y2;
!!FU&y2=-2:E;           Выход, если нет героя
!!BG:A?y3;              Тип действия
!!if&y3=3:;             Если защита
 !!HEy2:A1/?y4/4;       Проверяем наличие артефакта
 !!if&y4<>172:;
  !!HEy2:O?y5;
  !!OW:Iy5/?y6;        Проверяем статус ИИ
  !!if&y6=1:;          Запасная проверка для ИИ(Новые артефакты иногда просто игнорируются)
   !!HEy2:A2/172/?y7/?y8;
   !!VRy4&y7>0:S172;
  !!en:;
 !!en:;
 !!FU(WoGUE.Artifacts.EnchancedShield)&y4=172:P;
!!el:;
 !!if&y3=6|y3=7:;       Если атака или выстрел
  !!HEy2:A1/?y4/0;      Проверяем наличие артефакта
  !!if&y4<>173:;
   !!HEy2:O?y5;
   !!OW:Iy5/?y6;        Проверяем статус ИИ
   !!if&y6=1:;          Запасная проверка для ИИ(Новые артефакты иногда просто игнорируются)
    !!HEy2:A2/173/?y7/?y8;
    !!VRy4&y7>0:S173;
   !!en:;
  !!en:;
  !!FU(WoGUE.Artifacts.TerrifyingHelmet)&y4=173:P;
 !!en:;
!!en:;

!?FU(WoGUE.Artifacts.EnchancedShield);
!!BG:N?y1;              Номер стека
!!VRy2:S0;
!!VRy2:R3;              Cлучайное число
!!BMy1:P?y3;            Позиция
!!BMy1&y2=0:C27/y3/1/3/1;    Щит
!!BMy1&y2=1:C46/y3/1/3/1;    К. кожа
!!BMy1&y2=2:C49/y3/1/3/1;    Радость
!!BMy1&y2=3:C43/y3/1/3/1;    Жажда крови

!?FU(WoGUE.Artifacts.TerrifyingHelmet);
!!BG:N?y1;
!!BMy1:T?y2;
!!FU&y2<150/y2>145:E;   Боевые машины не устрашают
!!FU&y2=254:E;
!!BG:E?y3;              Цель
!!FU&y3=-1:E;           Выход, если отсутствует
!!BMy3:T?y4;
!!FU&y4=132:E;          Выход, если это Лазурный дракон
!!FU&y4>149/y4<197:E;   Выход, если это существо WoG
!!FU&y4=202:E;          Выход, если это Серафим
!!FU&y4=225:E;          Выход, если это Троглодит-Изверг
!!FU&y4=231:E;          Выход, если это Дракон Бездны
!!FU&y4=242:E;          Выход, если это Ящер-лейтинант
!!FU&y4=252:E;          Выход, если это Дух Энергии
!!FU&y4=254:E;          Выход, если это Требушет

!!VRy5:S1;
!!VRy5:R3;              Cлучайное число
!!FU&y5<>3:E;           Выход, если не повезло

!!BMy3:R?y6;
!!FU&y6=0:E;            Выход, если отряд не может сдать сдачу
!!BMy3:R0;              Если отряд не сдавал сдачу, убираем ее возможность
!!BA:Q?y7;              Проверяем статус быстрой битвы
!!if&y7=0:;
 !!VRz10:S^FEAR.WAV^;
 !!SN:Pz10;             Звук Страха
 !!BMy3:V15;            Анимация страха
 !!VRz1:Sz125223;
 !!BU:Mz1;
!!en:;


; Для старого кода
!?BA0;
!!BA:O?v6904/?v6905;    Хозяева



!?MF1;
!!BG:Q?v1;
!!VRv4&v1=0:S1;         Инвертируем сторону, чтобы узнать защитника
!!VRv4&v1=1:S0;
!!BA:Hv4/?v2;
!!FU&v2=-2:E;           Выход, если нет героя

!!HEv2:A1/?v3/5;        Проверяем наличие кольчуги
!!VRy23:S0;
!!OW&v4=0:Iv6904/?y23;  Проверяем статус ИИ
!!OW&v4=1:Iv6905/?y23;  Проверяем статус ИИ
!!HEv2&y23=1:A2/174/?y24/?y99;
!!VRv3&y23=1/y24>0:S174;Запасная проверка для ИИ(Новые артефакты иногда просто игнорируются)

!!FU&v3<>174:E;         Выход, если нет

!!MF:D?v5;              Урон, который получит отряд
!!VRe6:Sv5;
!!VRe6::8;
!!VRy7:Sv5;
!!VRy7:-e6;
!!VRy7&y7=v5:-1;
!!MF:Fy7;               Откоректированный урон
!!VRz2:Sz125236;
!!BU:Mz2;

!!BG:N?y1;              Текущий отряд
!!MF:N?y2;              Отряд, получающий урон
!!FU&y1=y2:E;           Исключаем ситуацию со рвом, минами и тд
!!BG:A?y3;              Тип действия
!!FU&y3=1:E;            Выходим, если магия или стрельба
!!FU&y3=7|y3=10:E;
!!VRy8:Sv5 -y7;
!!BMy1:Ky8;
!!VRz3:Sz125224;
!!BU:Mz3;



!?BR&v997>0;                   Триггер для нападающего
!!BA:H0/?n;
!!HEn:A1/?y2/1;                Проверяем наличие Мантии
!!OW:Iv6904/?y23;              Проверяем статус ИИ
!!HEn&y23=1:A2/176/?y24/?y99;
!!VRy2&y23=1/y24>0:S176;       Запасная проверка для ИИ(Новые артефакты иногда просто игнорируются)
!!FU&y2<>176:E;

!!HEn:Fd0/d0/d0/?y3;           Знания героя
!!VRy3:*10;                    Умножаем на 10
!!HEn:S24/?y4;                 Статус интеллекта
!!VRy3&y4=3:*2;                Проводим манипуляции в зависимости от уровня
!!VRy3&y4=2:*3;
!!VRy3&y4=2::2;
!!VRy3&y4=1:*5;
!!VRy3&y4=1::4;
!!HEn:I?y5/1;                  Текущее кол-во очков маны

!!HEn&y3>y5:Id1/1;             Добавляем одно, если не максимум

!?BR&v997>0;                   Триггер для защитника
!!BA:H1/?m;
!!FU&m=-2:E;                   Выход, если нет защитника
!!HEm:A1/?y2/1;                Проверяем наличие Мантии
!!OW:Iv6905/?y25;              Проверяем статус ИИ
!!HEm&y25=1:A2/176/?y26/?y99;
!!VRy2&y25=1/y26>0:S176;       Запасная проверка для ИИ(Новые артефакты иногда просто игнорируются)

!!FU&y2<>176:E;

!!HEm:Fd0/d0/d0/?y3;           Знания героя
!!VRy3:*10;                    Умножаем на 10
!!HEm:S24/?y4;                 Статус интеллекта
!!VRy3&y4=3:*2;                Проводим манипуляции в зависимости от cтатусa
!!VRy3&y4=2:*3;
!!VRy3&y4=2::2;
!!VRy3&y4=1:*5;
!!VRy3&y4=1::4;
!!HEm:I?y5/1;                  Текущее кол-во очков маны

!!HEm&y3>y5:Id1/1;             Добавляем одно, если не максимум


!?BF;
!!VRy8:S0;
!!VRy77:S0;

!!BA:H0/?v1;
!!BA:H1/?v2;
!!OW:Iv6904/?y83;              Проверяем статус ИИ

!!HEv1:A1/?y3/9;               Проверяем наличие рога
!!HEv1:A1/?y4/10;
!!HEv1:A1/?y5/11;
!!HEv1:A1/?y6/12;
!!HEv1:A1/?y7/18;

!!VRy77&y3=175:S1;             Если есть - ставим y77 на 1
!!VRy77&y4=175:S1;
!!VRy77&y5=175:S1;
!!VRy77&y6=175:S1;
!!VRy77&y7=175:S1;
!!HEv1&y83=1:A2/175/?y84/?y99;
!!VRy77&y83=1/y84>0:S1;        Запасная проверка для ИИ(Новые артефакты иногда просто игнорируются)

!!HEv1&y77>0:E?y99/?y8/1;      Получаем уровень героя
!!VRy8&y77>0:*2;               Умножаем на 2

!!BU&y77>0:S204/y8/03/0/-1/1;  Вызываем Сатиров
!!BU&y77>0:S207/y8/173/0/-1/1; Вызываем Фавнов

!!FU&v2=-2:E;                  Выход, если нет защитника
!!OW:Iv6905/?y64;              Проверяем статус ИИ

!!VRy8:S0;
!!VRy78:S0;

!!HEv2:A1/?y3/9;               Проверяем наличие рога
!!HEv2:A1/?y4/10;
!!HEv2:A1/?y5/11;
!!HEv2:A1/?y6/12;
!!HEv2:A1/?y7/18;

!!VRy78&y3=175:S1;             Если есть - ставим y78 на 1
!!VRy78&y4=175:S1;
!!VRy78&y5=175:S1;
!!VRy78&y6=175:S1;
!!VRy78&y7=175:S1;
!!HEv2&y64=1:A2/175/?y65/?y99;
!!VRy78&y64=1/y65>0:S1;        Запасная проверка для ИИ(Новые артефакты иногда просто игнорируются)

!!HEv2&y78>0:E?y99/?y8/1;      Получаем уровень героя
!!VRy8&y78>0:*2;               Умножаем на 2

!!BU&y78>0:S204/y8/13/1/-1/1;  Вызываем Сатиров
!!BU&y78>0:S207/y8/183/1/-1/1; Вызываем Фавнов




!?BF;
!!VRy67:S0;

!!BA:H0/?y1;
!!BA:H1/?y2;

!!HEy1:A1/?y3/6;        Проверяем наличие кольца
!!HEy1:A1/?y4/7;

!!VRy67&y3=178:S1;      Если есть - ставим y67 на 1
!!VRy67&y4=178:S1;
!!OW:Iv6904/?y53;       Проверяем статус ИИ
!!HEy1&y53=1:A2/178/?y54/?y99;
!!VRy67&y53=1/y54>0:S1; Запасная проверка для ИИ(Новые артефакты иногда просто игнорируются)
!!if&y67=1:;
 !!re i/0/20:;
  !!BMi:T?y69;
  !!if&y69>-1:;
   !!BMi:F?y70;
   !!VRy70:&64;         Не даем скорость боевым машинам
   !!BMi&y70=0:Sd1;
  !!en:;
 !!en:;
!!en:;

!!FU&y2=-2:E;           Выход, если нет защитника

!!HEy2:A1/?y37/6;       Проверяем наличие кольца
!!HEy2:A1/?y47/7;

!!VRy68:S0;
!!VRy68&y37=178:S1;     Если есть - ставим y68 на 1
!!VRy68&y47=178:S1;
!!OW:Iv6905/?y55;       Проверяем статус ИИ
!!HEy2&y55=1:A2/178/?y56/?y99;
!!VRy68&y55=1/y56>0:S1; Запасная проверка для ИИ(Новые артефакты иногда просто игнорируются)
!!if&y68=1:;
 !!re i/21/41:;
  !!BMi:T?y69;
  !!if&y69>-1:;
   !!BMi:F?y70;
   !!VRy70:&64;         Не даем скорость боевым машинам
   !!BMi&y70=0:Sd1;
  !!en:;
 !!en:;
!!en:;
---------------------------------------------------------------------------------------------------------------------------------

*Магические абилки существ
!?BA0;
!!SN:W^WoGUE.Battle.LastActingCreature^/-1;
!!SN:W^WoGUE.Battle.PositionToAttack^/-1;

*Заклинания перед атакой
!?BG0;
!!BG:A?n;
!!FU&n<>6/n<>7:E; Выход, если не удар и не выстрел
!!BG:N?y1;
**SN:W^WoGUE.Battle.LastActingStack^/y1;
!!BMy1:T?y2;
**BG:D?y3;
**SN:W^WoGUE.Battle.PositionToAttack^/y3;
!!if&y2=122:; Морской Змей
 !!FU(CreatureCastsMagic):Py1/10/32/3; 10% шанс колдовства Защиты от Воды
 !!FU:E;
!!en:;
!!if&y2=169:; Фанатик войны
 !!FU(CreatureCastsMagic):Py1/20/41/3; 20% шанс колдовства Благословения
 !!FU:E;
!!en:;
!!if&y2=196:; Драголич
 !!FU(CreatureCastsMagic):Py1/40/50/3; 40% шанс колдовства Печали
 !!FU:E;
!!en:;
!!if&y2=202:; Серафим
 !!FU(CreatureCastsMagic):Py1/20/48/3; 20% шанс колдовства Молитвы
 !!FU(CreatureCastsMagic):Py1/20/49/3; 20% шанс колдовства Радости
 *!FU(CreatureCastsMagic):Py1/5/26/1; 5% шанс колдовства базового Армагеддона
 !!FU:E;
!!en:;
!!if&y2=207:; Эльф-Фавн
 !!FU(CreatureCastsMagic):Py1/20/51/3; 20% шанс колдовства Удачи
 !!FU:E;
!!en:;
!!if&y2=230:; Проклятый Минотавр
 !!FU(CreatureCastsMagic):Py1/10/42/3; 10% шанс колдовства Проклятия
 !!FU:E;
!!en:;
!!if&y2=234:; Орк-гладиатор
 !!FU(CreatureCastsMagic):Py1/10/33/3; 10% шанс колдовства Защиты от Земли
 ; Вызов землетрясения
 !!FU&y1>20:E; Выход, если существо защитника
 !!BA:S?y3;
 !!FU&y3=0:E; Выход, если не осада
 !!VRy4:S0 R20;
 !!BMy1&y4<5:C14/0/2/2/0; 20% шанс колдовства
 !!FU:E;
!!en:;
!!if&y2=235:; Повелитель Огров
 !!FU(CreatureCastsMagic):Py1/10/43/3; 10% шанс колдовства Жажды Крови
 !!FU:E;
!!en:;
!!if&y2=237:; Громовой Дракон
 !!FU(CreatureCastsMagic):Py1/10/30/3; 10% шанс колдовства Защиты от Воздуха
!!en:;

*Заклинания после атаки
*?BG1;
**SN:W^WoGUE.Battle.LastActingStack^/?y1; Получаем номер последнего ходившего стека
**FU&y1=-1:E; Выход, если стек не был активен
**BMy1:N?v1;
**FU&v1<1:E;
**SN:W^WoGUE.Battle.LastActingStack^/-1;
**BMy1:T?y2; Получаем тип существа и колдуем магию
**SN:W^WoGUE.Battle.PositionToAttack^/?y3;
**if&y2=132:; Лазурный Дракон
 **FU(CreatureCastsMagic_Advanced):Py1/20/59/1/y3; 20% шанс колдовства базового Берсерка
 **FU:E;
**en:;
**if&y2=213:; Суккуб
 **FU(CreatureCastsMagic_Advanced):Py1/20/52/1/y3; 20% шанс колдовства базовой Неудачи
 **FU:E;
**en:;
**if&y2=224:; Механизированный Зомби
 **FU(CreatureCastsMagic_Advanced):Py1/20/54/1/y3; 20% шанс колдовства продвинутого Замедления
 **FU:E;
**en:;
**if&y2=226:; Смертельный Глаз
 **FU(CreatureCastsMagic_Advanced):Py1/50/61/2/y3; 50% шанс колдовства продвинутого Забвения
 **FU:E;
**en:;
**if&y2=236|y2=237:; Громовой Птероящер и Громовой Дракон
 **FU(CreatureCastsMagic_Advanced):Py1/20/19/1/y3; 20% шанс колдовства базовой Цепи Молний
 **FU:E;
**en:;
**if&y2=241|y2=242:; Ящер-Гвардеец и Яшер-Лейтинант
 **FU(CreatureCastsMagic_Advanced):Py1/20/47/2/y3; 20% шанс колдовства продвинутого Разрушающего Луча
 **FU:E;
**en:;
**if&y2=248|y2=250:; Морская Нимфа и Дух Океана
 **FU(CreatureCastsMagic_Advanced):Py1/20/50/1/y3; 20% шанс колдовства базовой Печали
**en:;

!?FU(CreatureCastsMagic);
; x1 - Acting stack
; x2 - Chance in %
; x3 - Spell ID
; x4 - Power (0-3)

!!VRy4:S0 R99;
!!BMx1&y4<x2:Cx3/0/3/x4/0;

!?FU(CreatureCastsMagic_Advanced);
; x1 - Acting stack
; x2 - Chance in %
; x3 - Spell ID
; x4 - Power (0-3)

!!UN:C6919200/4/?y1;
!!if&x5<>0:;
 !!BMx1:Z?y2;
 !!BG:Q?y3;
 !!SN:E5931936/2/y1/x3/y3/y2/0/1/1;
 *!IF:M^M %V1^;
 *!FU&v1<1:E;
!!en:;
!!VRy4:S0 R99;
!!IF:M^Ch %Y4 L %X4^;
*!BMx1&y4<x2:Cx3/x5/3/x4/0;
!!SN:E5898560/2/y1/x3/x5/1/0/x4/1;


*Астральные Элементали вызывают своих клонов
!?BR&v997>0; При каждом раунде, начиная со второго
!!re v10/0/41:; Проходимся по стекам
 !!BMv10:T?y1; Тип монстра в текущем стеке
 !!if&y1=251:; Если Астральный Элементаль
  !!BMv10:F?y2; Получаем флаги
  !!VRy2:&8388608; Клон?
  !!co&y2<>0:; Пропускаем клонов
  !!BMv10:N?y3; Количество оставшихся монстров
  !!co&y3=0:; Пропускаем мертвые стеки
  **SN:W^WoGUE.CloneAbility.CountOfClonesByStack%V10^/?y4; Получаем текущее количество клонов
  **co&y4>2:; Пропускаем, если есть 3
  !!BMv10:P?y5; Позиция текущего стека
  !!UN:C6919200/4/?y6; Менеджер битвы
  !!VRy7&v10<21:S21692; Корректируем смещение
  !!VRy7&v10>20:S21696;
  !!UN:Cy6/y7/4/?y8; Узнаем количество стеков у текущей стороны
  !!SN&y8<20:E5926784/2/y6/y5/50; Если не 20, вызываем клон
 !!en:;
!!en:;

*?BF;
**re y1/0/41:;
 **SN:W^WoGUE.CloneAbility.CountOfClonesByStack%Y1^/0; У каждого стека свой счетчик клонов
**en:;


*Завершение боя
!?CM4;
!!CM:I?y1;
!!CM:F?y2;
!!FU&y1<>2005|y2=512:E;
!!IF:Q1/z125230;
!!FU&-1:E;
!!BG:Q?y3;
!!BU:Vy3;

!!BA:H0/?v1;
!!BA:H1/?v2;
!!FU&v2=-2:E;

!!HEv1:S11/?v3;
!!HEv2:S11/?v4;
!!FU&v3=0/v4=0:E;
!!HEv1:F?y17/?y18/?y15/?y19;
!!HEv2:F?y20/?y21/?y16/?y22;

!!HEv1&v3>0:S14/v6684;
!!HEv1&v3>0:S15/v6683;
!!HEv1&v3>0:S16/v6682;
!!HEv1&v3>0:S17/v6681;
!!HEv1&v3>0/y15<y16:Fy17/y18/v6676/y19;
!!FU&v4=0:E;

!!HEv2:S14/v6680;
!!HEv2:S15/v6679;
!!HEv2:S16/v6678;
!!HEv2:S17/v6677;
!!HEv2&y16<y15:Fy20/y21/v6675/y22;

*Демонесса ставит препятствие
!?BA0;
!!SN:W^CountOfCastsAttk^/0;
!!SN:W^CountOfCastsDfnd^/0;
!!SN:W^DMNActivated^/0;

!?MM0;                       Триггер наведения мышки на участок поля боя
!!SN:W^DMNActivated^/?k;
!!FU&k=0:E;                  Выход, если режим установки не активирован
!!MM:D?y1;                   Получаем позицию под курсором
!!FU&y1<0|y1>185:E;          Выход, если курсор за пределами поля боя
!!BU:Dy1/?y2;
!!FU&y2<>-1:E;               Выход, если на позиции кто-то есть
!!BU:Oy1/?m;
!!FU&m<>0:E;                 Выход, если препятствие
!!BG:Q?y3;
!!BG:N?y6;
!!BMy6:T?y7;
!!FU&y7<>177/y7<>186:E;      Выход, если ходит не Демонесса
!!UN:R5/3/0;                 Меняем внешний вид курсора
!!VRz2:Sz125235;
!!MM:Mz2;                    Показываем сообщение

!?CM4;
!!SN:W^DMNActivated^/?k;
!!FU&k=0:E;
!!MM:D?y1;
!!SN&y1<0|y1>185:W^DMNActivated^/0;
!!UN&y1<0|y1>185:R5/2/6;
!!FU&y1<0|y1>185:E;
!!CM:F?n;
!!SN&n=512:W^DMNActivated^/0;
!!UN&n=512:R5/2/6;
!!FU&n<>0:E;
!!BU:Dy1/?y2;
!!FU&y2<>-1:E;
!!BU:Oy1/?m;
!!FU&m<>0:E;
!!BG:Q?y3;
!!BG:N?y6;
!!BMy6:T?y7;
!!FU&y7<>177/y7<>186:E;
!!BF:O79/y1;
!!SN&y3=0:W^DMNCountOfCastsAttk^/1;
!!SN&y3=1:W^DMNCountOfCastsDfnd^/1;
!!SN:W^DMNActivated^/0;
!!VRz1:S^VISIONS^;
!!SN:Pz1;
!!CM:R0;
!!UN:R5/2/0;
!!VRz2:Sz125234;
!!BU:Mz2;
!!BU:R;

!?CM4;
!!SN:W^DMNActivated^/?k;
!!FU&k=1:E;
!!CM:F?l;
!!FU&l<>1:E;
!!MM:D?y1;
!!FU&y1<0|y1>185:E;
!!BU:Ey1/?y2;
!!FU&y2<0:E;
!!BMy2:T?y3;
!!BG:Q?y4;
!!SN&y4=0:W^DMNCountOfCastsAttk^/?y5;
!!SN&y4=1:W^DMNCountOfCastsDfnd^/?y5;
!!FU&y5=1:E;            Выход, если заклинание израсходовано
!!SN&y3=177/y4=0:W^DMNActivated^/1;
!!SN&y3=186/y4=1:W^DMNActivated^/1;
!!CM&y3=177/y4=0:R0;
!!CM&y3=186/y4=1:R0;

*Новая анимация выстрела
!?MF1; Перед получением урона
!!UN:C42149568/4/?y1; Тип атаки
!!FU&y1<>4455011:E; Выход, если не выстрел
!!BG:N?y2; Активный стек
!!BMy2:T?y3; Тип монстра в нем
!!FU&y3<>177/y3<>186:E; Выход, если не Демонесса
!!BA:Q?y4; Получаем статус быстрой битвы
!!FU&y4<>0:E; Выход, если не быстрая
!!VRz1:S^DMNSSHOT_Extra^; Выбираем звук
!!MF:N?y5; Атакуемый стек
!!SN:R^C05SPE0.def^/^hatespike.def^; Подменяем графику
!!SN:Pz1; Проигрываем звук
!!BMy5:V10; Проигрываем новую анимацию
!!SN:R^C05SPE0.def^/^C05SPE0.def^; Восстанавливаем графику

*Вызов сообщения о версии
!?CM5;
!!CM:F?y1;
!!CM:I?y2;
!!IF&y1=1/y2=38:M1/z125217;

*Грааль Некрополиса творит прoклятие
!?BA0;
!!VRv6797:S0;           Статус Некрополиса
!!VRv276:S0;            Статус заклинания
*!VRv6774:S0;           Статус Башни

!?BF;
!!BA:S?v1;              Тип битвы
!!FU&v1<1:E;            Выход, если не осада города
!!FU&v1>3:E;
!!BA:P?v2/?v3/?v4;      В v2 v3 v4 координаты на карте
!!VRv3:-1;              Смещение на шаг вверх для получения позиции города, а не героя
!!OBv2/v3/v4:U?v5;      В v5 получить подтип объета (тип города)
!!OBv2/v3/v4:T?v6;
!!FU&v6<>98:E;
*!VRv6774&v1<>1/v5=2:S1;Отмечаем Башню, чтобы мины восстанавливались
!!FU&v5<>4:E;

!!CAv2/v3/v4:B3/26;     Проверить, построен ли храм грааля
!!FU&-1:E;              Выход, если не построен
!!VRv6797:S1;

!?BG0&v997=0/v6797=1;   Eсли первый раунд и город подходит
!!FU&v276>0:E;          Выход, если заклинание использовано
!!BG:N?v1;
!!BMv1:C42/00/3/3/0;
!!VRv276:+1;
!!VRz8:S125229;
!!BU:Mz8;

*Бандит-Главарь усиливается перед каждым ходом
!?BG0;
!!BG:A?y1;
!!if|y1=2/y1=3/y1=6:;
 !!BG:N?y2;
 !!BMy2:T?y3;
 !!BMy2&y3=301:Sd1 U1/d2 U2/d2 Ad1 Dd1;
!!el:;

*Требушет снижает защиту цели
 !!if&y1=7:; Если выстрел
  !!BG:N?y2;
  !!BMy2:T?y3; Кто атакует
  !!if&y3=254:; Если Требушет
   !!BG:E?y4; Смотрим, на кого направлен
   !!BMy4:D?y5; Актуальная защита
   !!VRy5&y5>5::-6; Если больше чем 5 делим на -6
   !!VRy5&y5>0/y5<6:S-1; Иначе ставим -1
   !!BMy4:Ddy5; Вычитаем y5 (в переменной отрицательное число или 0)
  !!en:;
 !!en:;
!!en:;

; Волколак боится огня (реализация в плагине)
!?MF1;
!!MF:N?y1;
!!BMy1:T?y2;
!!if&y2=194:;
 !!BG:N?y3;
 !!BMy3:T?y4;
 !!BG:A?y5;
 !!if&y5=7:;
  !!FU&y4<>44/y4<>45/y4<>171:E; Выход, если не стреляет Гог, Магог или Закаленный Снайпер
 !!en:;
 !!if&y5=6:;
  !!FU&y4<>52/y4<>53/y4<>114/y4<>130/y4<>131/y4<>158/y4<>212/y4<>216/y4<>218:E; Выход, если не бьют Ифриты, Огненные Элементали, Фениксы, Огненные Кобры или Адские Кони
 !!el:;
  !!FU:E;
 !!en:;
 !!VRz1:S^Волколаки в панике! Получаемый урон увеличен на 60%!^;
 !!BU:Mz1; Показываем сообщение в лог
!!en:;

*?BG1;
*!BG:N?y1;
*!BMy1:T?y2;
*!BMy1:K1000;
*!BMy1:C62/y2/3/3/0;
*!IF&y2=149:M^%Y2^;

*Обычная специальность на баллисте вызывает вылет, потому в начале игры она будет заменяться аналогом
*Больше не вызывает, но менять уже лень :)
!?FU(OnGameEnter);
!!HE6:X4/146/5/5/3;
!!HE36:X4/146/5/5/3;
!!HE54:X4/146/5/5/3;
!!HE81:X4/146/5/5/3;
!!HE97:X4/146/5/5/3;
!!HE118:X4/146/5/5/3;

*Тифон ставит запрещенным существам уровень -1, что ведет к вылетам. Исправляем!
!?FU(OnGameEnter);
!!re v1/0/256:;
 !!MA:Lv1/?y1;
 !!MA&y1<0:Lv1/0;
!!en:;


****************************************************************************************************
*Creature_Specialist Third Upgrade Mod* A Mod for ERA 2.9
*Author PerryR
*Sets new description for all creature Specialists and gives them a scaling, script taken from Advanced Classes Mod
*Disabled WoG Script "39 wog - hero specialization boost"

*?CM2&1000;                             [Show Correct Info for Creature Specialists]
**SN:W^Advanced_Classes_Mod_Active^/?y1; **FU&y1=1:E; [Exit if ACM mod active]

**CM:F?v3 I?v4 S?v5 T?v6;               [where we click and click subtype]
**FU|v4<>118/v6<>512/v5=13:E;           [If not Specialty Icon Clicked Exit]
**if|v3=512/v3=0:;                      [if right or left click]
**HE-1:N?y6;                            [Get current hero #]
**FU&y6>=144:E;                         [Exit for Extension Heroes]
**HEy6:X?y11/?y12/?y13/?y14/?y15/?y16/?y17; [Check Hero Speciality]
**FU&y11<>1:E;                          [Exit if no Creature_Specialist]

**CM:R0;                                [disable standard reaction]
**HEy6:R2/?y70;
**HEy6:B0/?z1;
**HEy6:E?y47/?y48/1;                    [Check hero Level]
**SN&y70=0:T^Third_Upgrade_Mod.his^/?z5;
**SN&y70=1:T^Third_Upgrade_Mod.her^/?z5;

**FU(Set_Creature_Upgrades_1):Py12/y6/?y16/?y17;

**UN:N3/z3/y12/1;                      [Normal Creature]
**UN:N3/z4/y16/1;                      [First Upgrade]
**UN:N3/z6/y17/1;                      [Second Upgrade]

**MA:Ly12/?y14;                         [Get creature level]
**VRy60&y14=0:S16; **VRy60&y14=1:S15; **VRy60&y14=2:S13; **VRy60&y14=3:S12; **VRy60&y14=4:S10; **VRy60&y14=5:S7; **VRy60&y14=6:S5;
**VRy14:+1;
**VRy50:Sy48:7+1;                       [7 Attack and Defense per Level]
**VRy51:Sy48:y60;                       [Damage per Level based on creature level]
**VRy52:Sy48+9;                         [+1% per Health per Level +9%]

**SN:T^Third_Upgrade_Mod.Creature_Specialist^/?s^Text^/^level^/y14/^attack^/y50/^damage^/y51/^health^/y52/^Crea1^/z3/^Crea2^/z4/^Crea3^/z6/^Hero^/z1/^sex^/z5;

**IF:M1/4/^%S(Text)^;
**en:;


*?BF;
**SN:W^Advanced_Classes_Mod_Active^/?y1; **FU&y1=1:E; [Exit if ACM mod active]

**BH0:N?y1;                             [Attacker]
**FU(Third_Upgrade_1)&y1>=0/y1<=155:Py1/1;

**BH1:N?y1;                             [Defender]
**FU(Third_Upgrade_1)&y1>=0/y1<=155:Py1/2;

*?FU(Third_Upgrade_1);
**HEx1:X?y11/?y12/?y13/?y14/?y15/?y16/?y17; [Check Hero Speciality]
**FU&y11<>1:E;                          [Exit if no Creature Specialist]

**HEx1:E?y47/?y48/1;                    [Check hero Level]
**MA:Ly12/?y14;
**VRy60&y14=0:S16; **VRy60&y14=1:S15; **VRy60&y14=2:S13; **VRy60&y14=3:S12; **VRy60&y14=4:S10; **VRy60&y14=5:S7; **VRy60&y14=6:S5;

**FU(Set_Creature_Upgrades_1):Py12/x1/?y13/?y14;

**VRy50:Sy48:7+1;                       [Attack and Defense per 7 Level]
**VRy51:Sy48:y60;                       [Damage per Level]
**VRy52:Sy48+9 +100;                    [+1% per Health per Level +9%]
**DO(Third_Upgrade_0)/0/20/1&x2=1:Py12/y13/y50/y52/y51/y14; [Pass Creature, Upgraded Creature, Att/Def, Health and Damage]
**DO(Third_Upgrade_0)/21/41/1&x2=2:Py12/y13/y50/y52/y51/y14; [Pass Creature, Upgraded Creature, Att/Def, Health and Damage]


*?FU(Third_Upgrade_0);

**BMx16:T?y1;
**if|y1=x1/y1=x2/y1=x6:;                [If Special Creature or Upgraded Creature is found increase stats]
**BMx16:Adx3;                           [Attack]
**BMx16:Ddx3;                           [Defense]
**BMx16:H?y2; **VRy2:*x4:100;           [Get Health]
**BMx16:Hy2;
**BMx16:U1/dx5 U2/dx5;                  [Min+Max Damage]
**en:;




---------------------------------------------------------------------------------------------------------------------------------

!?FU25002;              Обнуление линеек опыта
!!VRy1:Sx16 -1;
!!EAx1:By1/0/////////////;
