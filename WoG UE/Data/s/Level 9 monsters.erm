ZVSE

*Серафимы и Драголичи
*А также Драконы Бездны, Духи Энергии и, возможно, кто-то еще :)
!#SN:W^SeraphStatus^/0;
!#SN:W^DragolichStatus^/0;
!#SN:W^AbyssDragonStatus^/0;
!#SN:W^ElectroPhoenixStatus^/0;

!?FU15042;
!!HE-1:E?n/?y1;     Уровень героя
!!VRy2:S0;          Количество типов потенциально улучшаемых существ, которых нельзя улучшить
!!VRy20:S0;         Количество типов потенциально улучшаемых существ, которых можно улучшить

!!MA&y1<12:U150/-2; Верховные Архангелы не улучшаются
!!MA&y1>11:U150/202;
!!MA&y1<10:U154/-2; Кровавые Драконы не улучшаются
!!MA&y1>9:U154/196;
!!MA&y1<11:U155/-2; Темные Драконы не улучшаются
!!MA&y1>10:U155/231;
!!MA&y1<11:U158/-2; Духи Погребального костра не улучшаются
!!MA&y1>10:U158/252;

!!HE-1:C1/150/?n/?y3; Получаем список потенциально улучшаемых существ
!!HE-1:C1/154/?n/?y4;
!!HE-1:C1/155/?n/?y5;
!!HE-1:C1/158/?n/?y6;
--------------------------------------------------------------------------------

* Подсчет существ

; Если есть Верховные Архангелы
!!if&y3>0:;
 !!VRy2&y1<12:+1;   Если уровень меньше 12 (улучшить нельзя) увеличиваем счетчик
 !!VRy20&y1>11:+1;  Если уровень больше 11 (улучшить можно) увеличиваем счетчик

 ; Запись сушеств в псевдомассивы (инициализация)
 !!VRz9&y2>0:Sz125243;   Если улучшить нельзя - z9
 !!VRz10&y20>0:Sz125243; Если улучшить можно - z10
!!en:;
--------------------------------------------------------------------------------
; Если есть Кровавые Драконы
!!if&y4>0:;
 !!VRy2&y1<10:+1;   Если уровень меньше 10 (улучшить нельзя) увеличиваем счетчик
 !!VRy20&y1>9:+1;   Если уровень больше 9 (улучшить можно) увеличиваем счетчик

 ; Определение разделителя
 !!if&y5=0/y6=0:;   Если остальных существ нет
  !!VRz1:S^ ^ +z125241 +^ ^; Ставим между словами " и "
 !!el:;             Иначе
  !!VRz1:S^, ^;     Ставим между словами запятую (", ")
  !!VRz1&y20=1/y1<11:Sz125241;
 !!en:;
 ; Запись сушеств в псевдомассивы
 !!VRz9&y2=1:Sz125244; Если "массив" пуст, записываем в переменную название существа
 !!VRz9&y2>1:+z1 +z125244; Иначе добавляем разделитель и приписываем
 !!VRz10&y20=1:Sz125244; Если "массив" пуст, записываем в переменную название существа
 !!VRz10&y20>1:+z1 +z125244; Иначе добавляем разделитель и приписываем
!!en:;
--------------------------------------------------------------------------------
; Если есть Темные Драконы
!!if&y5>0:;
 !!VRy2&y1<11:+1;   Если уровень меньше 11 (улучшить нельзя) увеличиваем счетчик
 !!VRy20&y1>10:+1;  Если уровень больше 10 (улучшить можно) увеличиваем счетчик

 !!VRz1:S^, ^; Следующий необходмый уровень совпадает с текущим, так что разделитель всегда ", ". " и " последует дальше (в следующем блоке)
 !!VRz1&y6=0:S^ ^ +z125241 +^ ^; Тем не менее, если существа отсутствуют, то надо все-таки ставить " и "
 ; Запись сушеств в псевдомассивы
 !!VRz9&y2=1:Sz125245;
 !!VRz9&y2>1:+z1 +z125245;
 !!VRz10&y20=1/y4=0:Sz125245; Без проверки на Кровавых Драконов название существа потеряется, так как в переменную уже записана единица, и код сработает (хотя не должен)
 !!VRz10&y20>1:+z1 +z125245;
!!en:;
--------------------------------------------------------------------------------
; Если есть Духи Погребального костра
!!if&y6>0:;
 !!VRy2&y1<11:+1;   Если уровень меньше 11 (улучшить нельзя) увеличиваем счетчик
 !!VRy20&y1>10:+1;  Если уровень больше 10 (улучшить можно) увеличиваем счетчик

 !!VRz1:S^ ^ +z125241 +^ ^; Необходмый уровень совпадает, так что разделитель всегда " и ". Также это гарантированно последнее существо в списке
 ; Запись сушеств в псевдомассивы
 !!VRz9&y2=1:Sz125246;
 !!VRz9&y2>1:+z1 +z125246;
 !!VRz10&y20=1/y4=0:Sz125246; Без проверки на Кровавых Драконов название существа потеряется, так как в переменную уже записана единица, и код сработает (хотя не должен)
 !!VRz10&y20>1:+z1 +z125246;
!!en:;
--------------------------------------------------------------------------------

* Показ сообщения для неулучшаемых
!!IF&y2>0:M1/z125242;

* Показ сообщения для улучшаемых
!!IF&y20>0:M1/z125240;

!?OB35;
!!UN:P650/?y1;
!!FU&y1=1:E; Выключаем механику в режиме SoD

!!if&-1000:; Бонусы для ИИ
 !!SN:W^SeraphStatus^/?y1;
 !!HE-1&y1=1:Y5/3/1/1;
 !!SN:W^DragolichStatus^/?y2;
 !!HE-1&y2=1:Y21/10000/1/1;
 !!SN:W^AbyssDragonStatus^/?y3;
 !!HE-1&y3=1:Y21/10000/1/1;
 !!SN:W^ElectroPhoenixStatus^/?y4;
 !!HE-1&y4=1:Y21/10000/1/1;
 
 !!MA:U150/202; Восстанавливаем возможность улучшать существ для ИИ
 !!MA:U154/196;
 !!MA:U155/231;
 !!MA:U158/252;
!!el:;
 !!FU15042&1000:P; Диалог для человека
!!en:;

!$OB35;
!!FU15042&1000:P;
!!MA:U150/-1; Делаем существ снова неулучшаемыми.
!!MA:U154/-1;
!!MA:U155/-1;
!!MA:U158/-1;
!!HE-1:N?y10;
!!FU(CheckForLVL9Monsters):Py10; Проверяем, не появилось ли в игре новое существо 9 уровня

!$OB6;
!!HE-1:N?y1;
!!FU(CheckForLVL9Monsters):Py1;

!$OB16;
!!HE-1:N?y1;
!!FU(CheckForLVL9Monsters):Py1;

!$OB17;
!!HE-1:N?y1;
!!FU(CheckForLVL9Monsters):Py1;

!$OB26;
!!HE-1:N?y1;
!!FU(CheckForLVL9Monsters):Py1;

!$OB33;
!!HE-1:N?y1;
!!FU(CheckForLVL9Monsters):Py1;

!$OB34;
!!HE-1:N?y1;
!!FU(CheckForLVL9Monsters):Py1;

!$OB78;
!!HE-1:N?y1;
!!FU(CheckForLVL9Monsters):Py1;

!$OB54;
!!HE-1:N?y1;
!!FU(CheckForLVL9Monsters):Py1;

!$OB53;
!!HE-1:N?y1;
!!FU(CheckForLVL9Monsters):Py1;

!$OB83;
!!HE-1:N?y1;
!!FU(CheckForLVL9Monsters):Py1;

!$OB98;
!!HE-1:N?y1;
!!FU(CheckForLVL9Monsters):Py1;

*#TM9:S1/999/7/255;
*?TM9;
!?FU(OnEveryDay);
!!re i/0/155:;
 !!HEi:O?y1;
 !!FU(CheckForLVL9Monsters)&y1<>-1:Pi; Ненанятых героев проверять смысла нет
!!en:;

!?FU(OnGameEnter);
!!re i/0/155:;
 !!HEi:O?y1;
 !!FU(CheckForLVL9Monsters)&y1<>-1:Pi; Ненанятых героев проверять смысла нет
!!en:;

!?FU(CheckForLVL9Monsters); Функция проверки появления существ 9 уровня (бывшая 309)
!!SN:W^SeraphStatus^/?y1;
!!SN:W^DragolichStatus^/?y2;
!!SN:W^AbyssDragonStatus^/?y3;
!!SN:W^ElectroPhoenixStatus^/?y4;
!!FU(CheckForLVL9Monsters2)&y1=0:Px1/202/125200;
!!FU(CheckForLVL9Monsters2)&y2=0:Px1/196/125201;
!!FU(CheckForLVL9Monsters2)&y3=0:Px1/231/125202;
!!FU(CheckForLVL9Monsters2)&y4=0:Px1/252/125239;

!?FU(CheckForLVL9Monsters2);
!!HEx1:C1/x2/?n/?y1;
!!if&y1>0:; Если у героя есть сушества
 !!SN&x2=202:W^SeraphStatus^/1;
 !!SN&x2=196:W^DragolichStatus^/1;
 !!SN&x2=231:W^AbyssDragonStatus^/1;
 !!SN&x2=252:W^ElectroPhoenixStatus^/1;
 !!IF:M1/zx3; Показ сообщения, если статус установился
!!en:;
