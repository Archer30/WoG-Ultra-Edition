ZVSE

**Расширения карты

*Новое событие в водовороте
!#VRv6799:C200;
!#VRv6783:C200;
!$OB111;
!!FU&-1000:E;
!!HE-1:N?y45;
!!VRy95:Sc;
!!VRy95:-7;
!!VRy45&y95>v6783:S200;
!!FU&y45=v6799:E;
!!VRy10:S0;
!!VRy10:R10;
!!FU&y10<>1:E;
!!HE-1:E?y53/?y54;
!!FU&y54<4:E;                Выход если уровень меньше чем 4
!!HE-1:C1/122/?y41/?n;
!!FU&n>0:E;                  Если у игрока есть Змеи - выходим

!!VRr:S0;
!!re i/0/6:;                 Вычисляем суммарное здоровье армии героя
 !!HE-1:C0/i/?y1/?y2;
 !!co&y1=-1:;
 !!MA:Py1/?y3;
 !!VRy3:*y2;
 !!VRr:+y3;
!!en:;
!!VRy21:Sr :225;             Находим нужное количество в соответвии с армией героя
!!VRy21:R2;
!!VRy21&y21=0:S1;
!!VRy21&y54>5:+y54 -4;
!!VRy21&y54>10:+1;
!!VRy21&y54>15:+3;
!!VRy21&y54>20:*2;
!!VRy54&y54>10:S10;
!!MA&y54>7:S122/y54;
!!HE-1:P?y50/?y51/?y52;
!!IF:M1/z163500;
!!VRv6783:Sc;
!!HE-1:N?v6799;
!!HE-1:Ty50/y51/y52/122/y21;

!$OB111;
!!MA:S122/7;
!!HE-1:E?y53/?y54;
!!FU&y54<3/1000:E;           Выход если уровень меньше чем 3
!!HE-1:N?y45;
!!VRy95:Sc;
!!VRy95:-5;
!!VRy45&y95>v6783:S200;
!!FU&y45=v6799:E;
!!VRy2:S0;
!!VRy2:R60;
!!IF&y2<5/1000:M1/z163501;
!!HE-1&y2<5:C2/122/1/1;
!!HE-1&y2<5:N?v6799;


*Новое событие в Покрове Тьмы
!$OB15&1000;
!!VRy1:S0;
!!VRy1:R60;
!!IF&y1<10:M1/z163502;
!!HE-1&y1<10:Y1/1/1/1;
!!HE-1&y1<10:Y8/1/3/1;


*Новое событие в обелиске
!#VRv6800:C0;
!#VRv6776:C200;
!$OB57;
!!FU&v6776=0:E;              Выход, если функция заблокирована
!!HE-1:A2/1000/?y1/?n;
!!FU&y1=0:E;                 Выход, если у героя нет книги
!!IF&1000:Q1/z163503;
!!FU&1000/-1:E;              Выход, если игрок отказался смотреть руны
!!SN:W^WoGUE.Obelisk%V998%V999%V1000.Setup^/?y2; Статус настройки обелиска
!!if&y2=-1:; Если потеря маны
 [:Obelisk.ThereIsAProblem]
 !!FU&-1000:E; ИИ не страдает
 !!IF:M1/z163504; Сообщение для игрока
 !!HE-1:Id-10; Забираем 10 очков
 !!HE-1:I?i; Проверка на отрицательное количество
 !!HE-1&i<0:I0; Ставим 0, если ушли в минус
 !!FU:E; Выходим из функции
!!en:;
!!if&y2=0:; Если обелиск не настроен
 ; По какой-то неизвестной мне причине генератор случайных чисел в этом триггере всегда стартует с одной и той же позиции...
 ; Решение проблемы - прогон до нужного места
 !!re i/0/v6800:;
  !!VRy2:S10;
  !!VRy2:R20; Генерируем число v6800 раз
 !!en:;
 !!VRv6800:+1; Запоминаем количество генераций
 !!if&y2>20:; Если не повезло
  !!SN:W^WoGUE.Obelisk%V998%V999%V1000.Setup^/-1; Сохраняем результат
  !!SN:G[Obelisk.ThereIsAProblem]; И прыгаем к обработчику
 !!en:;
 !!VRy2&y2=15:S47; Перенастраиваем некоторые заклинания
 !!VRy2&y2=18:S41;
 !!VRy2&y2=19:S42;
 !!VRy2&y2=20:S62;
 !!SN:W^WoGUE.Obelisk%V998%V999%V1000.Setup^/y2; Помечаем обелиск как настроенный
!!en:;
!!HE-1:My2/1; Добавляем заклинание в книгу
!!IF&1000:Q1/9/y2/1/z163505; Уведомляем игрока об изучении заклинания


*Выясняем количество врат на карте
!?PI;
!!UN:U103/-1/?v6781;

*Новое событие в Костре
!?OB12;
!!FU&v6781<1:E; Выход, если на карте нет врат
!!SN:W^WoGUE.Campfire%V998%V999%V1000.Setup^/?y1; Статус настройки костра
!!if&y1=0:; Если не настроен
 !!VRy1:S1; Берем случайное число
 !!VRy1:R8;
 !!SN:W^WoGUE.Campfire%V998%V999%V1000.Setup^/y1; Сохраняем результат
 !!FU&y1>6:E; Если не повезло - выход
 [:Campfire.Handle]
 !!if&1000:;
  !!IF:Q1/z163519; Если что-то есть, спрашиваем игрока о том, хочет ли он приблизиться
  !!OBv998/v999/v1000&-1:T125; Заменяем тип объекта (125 - водоросль)
  !!FU&-1:E; Выход, если не хочет
 !!en:;
 !!if&y1=6:; Дарим Ос
  !!FU(WoGUE.Campfire.GiveTroops):P163509/126;
  !!FU:E;
 !!en:;
 !!if&y1=5:; Дарим Пауков
  !!FU(WoGUE.Campfire.GiveTroops):P163508/124;
  !!FU:E;
 !!en:;
 !!FU&-1000:E; На ИИ не нападаем
 !!if&y1<3:;
  !!FU(WoGUE.Campfire.Battle):P163506/124; Бой с Пауками (Значения 1 и 2)
  !!FU:E;
 !!en:;
 !!FU(WoGUE.Campfire.Battle):P163507/126; Бой с Осами (Значения 3 и 4)
!!el:;
 !!SN:G[Campfire.Handle]; Прыгаем к обработчику, если костер уже настроен
!!en:;

!$OB12;
!!FU&v6781<1:E; Выход, если на карте нет врат
!!SN:W^WoGUE.Campfire%V998%V999%V1000.Setup^/?y1; Статус настройки костра
!!OBv998/v999/v1000&y1<7:T12; Если настроен, восстанавливаем тип объекта

!?FU(WoGUE.Campfire.GiveTroops);
!!VRy1:S5;
!!VRy1:R10; Сколько присоеденится
!!if&1000:;
 !!IF:M1/zx1;
 !!HE-1:C2/x2/y1/1; Если человек, спрашиваем
!!el:;
 !!HE-1:C2/x2/y1/0; ИИ даем сразу
!!en:;
!!SN:W^WoGUE.Campfire%V998%V999%V1000.Setup^/10; Отмечаем, что костер зачистили

!?FU(WoGUE.Campfire.Battle);
!!VRy1:S20;
!!VRy1:R25;                  Число нападающих
!!HE-1:P?y50/?y51/?y52;
!!IF:M1/zx1;
!!HE-1:Ty50/y51/y52/x2/y1;   Бой
!!HE-1:O?y2;                 Проверяем, победил герой или нет
!!SN&y2<>-1:W^WoGUE.Campfire%V998%V999%V1000.Setup^/10; Если победил (остался у владельца), отмечаем, что костер зачистили


*Новое событие в Подземных вратах
!#VRv6798:C200;                      Герой
!#VRv6784:C200;                      День

!?OB103;
!!VRy15:S0;
!!IF:V1/0 V2/0;
!!HE-1:C1/124/?y41/?y15;
!!IF&1000/y15>0:Q1/21/124/21/128/2/z163510;
!!HE-1&1|-1000:C1/124/128/d;

!!VRy15:S0;
!!HE-1:C1/126/?y41/?y15;
!!IF&1000/y15>0:Q2/21/126/21/128/2/z163511;
!!HE-1&2|-1000:C1/126/128/d;

!!FU&2:E;
!!FU&1|-1000:E;                      Выход, если игрок подкормил пожирателей(или это ИИ)
!!VRy10:S0;
!!VRy10:R70;
!!FU&y10>5:E;
!!HE-1:C1/128/?y41/?n;
!!FU&n>0:E;                          Если у игрока есть Пожиратели - выходим

!!HE-1:E?y53/?y54;                   Уровень героя
!!FU&y54<3:E;                        Выход если меньше чем 3
!!HE-1:N?y45;                        Номер героя
!!VRy95:Sc;                          Текущий день в y95
!!VRy95:-7;                          Забираем неделю
!!VRy45&y95>v6784:S200;              Если последний бой был раньше - обнуляем номер
!!FU&y45=v6798:E;                    Если номер совпал с предыдущим - выход
!!IF:M1/z163512;
!!VRv6784:Sc;                        Сохраняем дату
!!HE-1:N?v6798;                      Сохраняем номер
!!HE-1:P?y50/?y51/?y52;              Координаты

!!VRr:S0;
!!re i/0/6:;                         Вычисляем суммарное здоровье армии героя
 !!HE-1:C0/i/?y1/?y2;
 !!co&y1=-1:;
 !!MA:Py1/?y3;
 !!VRy3:*y2;
 !!VRr:+y3;
!!en:;
!!VRy11:Sr :50;                      Находим нужное количество в соответвии с армией героя
!!VRy11:R10;                         Случайное количество
!!VRy11&y54<6::2;                    Увеличиваем/уменьшаем в соответствии с уровнем
!!VRy11&y11=0:S2;
!!VRy11&y54>10:R15;
!!VRy11&y54>15::2 *3;
!!VRy11&y54>15:R10;
!!VRy54&y54>10:S10;
!!MA&y54>7:S128/y54;                 Поднимаем скорость пожирателям
!!HE-1:Ty50/y51/y52/128/y11;         Бой

*Возвращаем скорость
!$OB103;
!!MA:S128/7;


*Лесные Драконы
!#SN:W^UE_ForestDragonsAreOnTheMap^/0;
!#SN:W^UE_SwampDragonsAreOnTheMap^/0;
!#VRv1024:C2/7/1; Положение лесных драконов
!#VRv1027:C2/8/1; Положение болотных драконов


*?PI;
**UN:Iv1024/v1025/v1026/26/0;
**UN:I2/7/1/26/0;
**UN:I2/8/1/26/0;
**VRy1:S0 R1;
**VRy2:S0 R2;
**if&y1=1:;

**en:;
**if&y2=2:;

**en:;

*?FU(OnLocalEvent %V1024/%V1025/%V1026);
*?OBv1024/v1025/v1026;
**IF:M^^;

*?LE2/8/1;
**IF:M^^;



*Скелет забирает мораль
!?OB22&1000;
!!OBv998/v999/v1000:M-1/1/0;
!!SKv998/v999/v1000:A?y1;
!!VRz1:Sz163513;
!!VRz2:Sz163514;
!!IF&y1<>1023:Q1/8/y1/1/z1;
!!IF&y1<>1023:Q1/16/1/1^^;
!!IF&y1=1023:Q1/16/1/1/z2;
!!HE-1:R0/d-1;

*Возможность отказаться от бонуса Ученого
!?OB81;
!!FU&-1000:E;
!!OBv998/v999/v1000:M-1/1/0;         Отключаем стандартное сообщение
!!IF:V1/0;
!!VRv1040:S1044;
!!VRz1:Sz163515;
!!SCv998/v999/v1000:T?v1011;         Тип бонуса
!!SCv998/v999/v1000&v1011=0:P?y2;
!!SCv998/v999/v1000&v1011=1:S?y3;
!!SCv998/v999/v1000&v1011=2:L?y4;
!!VRv1012&v1011=0:Sy2;               Сохраняем на случай отказа
!!VRv1012&v1011=1:Sy3;
!!VRv1012&v1011=2:Sy4;
!!VRy2:+31;
!!VRy5:S0;                           Инициализируем переменную, чтобы случайно не интерпретировать мусор как нечто важное
!!HE-1&v1011=1:Sy3/?y5;              Если бонус - навык, проверяем его уровень у героя
!!VRy3:*3 +3 +y5;
!!VRz1&y5=3:Sz163516;
!!VRy3&y5=3:-1;                      Уменьшаем значение, чтобы не показать неправильную картинку
!!IF&y5=3:Q1/20/y3/1/z1;             Если уже экспертный, показываем сообщение
!!OBv998/v999/v1000&y5=3:T125;       Заменяем тип объекта (125 - водоросль)
!!VRv1040&y5=3:S1045;                Отмечаем
!!FU&y5=3:E;
!!VRv1:S0;
!!DO15053/0/27/1&v1011=1:P-1/1;      Считаем навыки героя
!!VRz1&y5=0/v1>7:Sz125236;
!!VRy3&y5=0/v1>7:-3;                 Уменьшаем значение, чтобы не показать неправильную картинку
!!IF&y5=0/v1>7:Q1/20/y3/1/z1;        Если уже есть 8, показываем сообщение
!!OBv998/v999/v1000&y5=0/v1>7:T125;  Заменяем тип объекта (125 - водоросль)
!!VRv1040&y5=0/v1>7:S1045;           Отмечаем
!!FU&y5=0/v1>7:E;
!!VRy4:-1024;
!!VRy6:S0;                           Инициализируем переменную, чтобы случайно не интерпретировать мусор как нечто важное
!!HE-1&v1011=2:My4/?y6;
!!VRz1&y6=1:Sz163516;
!!IF&y6=1:Q1/9/y4/1/z1;              Если у героя уже есть заклинание
!!OBv998/v999/v1000&y6=1:T125;       Заменяем тип объекта (125 - водоросль)
!!VRv1040&y6=1:S1045;                Отмечаем
!!FU&y6=1:E;
!!IF&v1011=0:Q1/y2/1/2/z1;
!!IF&v1011=1:Q1/20/y3/2/z1;
!!IF&v1011=2:Q1/9/y4/2/z1;
!!OBv998/v999/v1000&-1:T125;         Если игрок отказался, заменяем тип объекта (125 - водоросль)
!!VRv1040&-1:S1045;                  Отмечаем
!!FU&-1:E;
!!HE-1&v1011=0/y2=0:Fd1/d0/d0/d0;    Даем первичный навык
!!HE-1&v1011=0/y2=1:Fd0/d1/d0/d0;    Даем первичный навык
!!HE-1&v1011=0/y2=2:Fd0/d0/d1/d0;    Даем первичный навык
!!HE-1&v1011=0/y2=3:Fd0/d0/d0/d1;    Даем первичный навык
!!FU&v1011=0:E;
!!HE-1:Fd0/d0/d-1/d0;                Уничтожаем стандартный бонус (какой-то скрипт иногда подменяет его)
!!SCv998/v999/v1000:T0;
!!SCv998/v999/v1000:P2;
!!VRy5:+1;                           Повышаем уровень вторичного навыка
!!HE-1&v1011=1:Sv1012/y5;
!!HE-1&v1011=2:My4/1;                Даем заклинание

!$OB81;
!!FU&-1000:E;
!!FU&v1040<>1045:E;                  Если в этом есть необходимость, восстанавливаем ученого
!!OBv998/v999/v1000:T81;             Тип объекта снова 81
!!SCv998/v999/v1000:Tv1011;          Возвращаем тип бонуса
!!SCv998/v999/v1000&v1011=0:Pv1012;  Возвращаем сам бонус
!!SCv998/v999/v1000&v1011=1:Sv1012;  Возвращаем сам бонус
!!SCv998/v999/v1000&v1011=2:Lv1012;  Возвращаем сам бонус

!?FU15053;
!!HEx1:Sx16/?y1;
!!VRvx2&y1>0:+1;

*Усиление големов при входе в пирамиду
!?OB63/0;
*Золотой голем
!!MA:A116/11 P116/50 M116/8 E116/10;
*Алмазный голем
!!MA:A117/13 P117/60 M117/10 E117/14;

*Ослабление после битвы
!$OB63/0;
*Золотой голем
!!MA:A116/10 P116/40 M116/5 E116/8;
*Алмазный голем
!!MA:A117/12 P117/50 M117/6 E117/10;

*Водоворот
*?CM0;                  После клика мышкой
**CM:P?y1/?y2/?y3;      Позиция на карте
**OBy1/y2/y3:T?y4;      Объект на позиции
**FU&y4<>17:E;          Выход, если не жилище
**OBy1/y2/y3:U?y5;      Подтип
**FU&y5<>89:E;          Выход, если не 89

**DWy1/y2/y3:M0/?y6/?y7;Тип монстров слота 0 в y6, количество в y7
**FU&y6=122:E;          Выход, если уже 122

**DWy1/y2/y3:M0/122/1;  Ставим тип 122 и количество 1
**DWy1/y2/y3:G0/122/3;  Добавляеем защитников

*?OB17/89;              Триггер жилища
**HE-1:P?y1/?y2/?y3;    Позиция на карте
**DWy1/y2/y3:M0/?y4/?y5;Тип монстров слота 0 в y4, количество в y5
**FU&y4=122:E;          Выход, если уже 122

**DWy1/y2/y3:M0/122/1;  Ставим тип 122 и количество 1
**DWy1/y2/y3:G0/122/3;  Добавляеем защитников

*Адская крепость
!?OB16/20;              Триггер объекта
!!HE-1:P?y1/?y2/?y3;    Позиция на карте
!!CBy1/y2/y3:T?y4;      Узнаем статус посещения
!!FU&y4=1:E;            Выход, если посещен
!!CBy1/y2/y3:G0/54/5;   Ставим стражников на позиции
!!CBy1/y2/y3:G3/52/10;
!!CBy1/y2/y3:G2/53/7;
!!CBy1/y2/y3:G1/55/3;
!!CBy1/y2/y3:G4/153/1;
!!CBy1/y2/y3:G5/218/5;
!!CBy1/y2/y3:R2/0;      Удаляем руду из списка наград
!!CBy1/y2/y3:A1/?y5;    Получаем список артефактов
!!CBy1/y2/y3&y5=3:A4/0; Удаляем, если есть(если этого не делать, артефакты копятся, когда игрок посещет объект, но не идет в бой)
!!CBy1/y2/y3&y5=3:A4/1;
!!CBy1/y2/y3&y5=3:A4/2;
!!CBy1/y2/y3:R1/10;     10 ртути
!!CBy1/y2/y3:R3/10;     10 серы
!!CBy1/y2/y3:R6/20000;  Золото
!!VRy5:S0 R3;           Случайное число
!!CBy1/y2/y3&y5=0:A3/11;Настраивам артефакт в зависимости от числа
!!CBy1/y2/y3&y5=1:A3/17;
!!CBy1/y2/y3&y5=2:A3/23;
!!CBy1/y2/y3&y5=3:A3/29;
!!VRy5:S0 R1;           Случайное число
!!CBy1/y2/y3&y5=0:A3/70;Настраивам артефакт в зависимости от числа
!!CBy1/y2/y3&y5=1:A3/98;
!!CBy1/y2/y3:A3/108;    Заодно даем значек смелости

*Твердыня Титана
!?OB16/12;              Триггер объекта
!!HE-1:P?y1/?y2/?y3;    Позиция на карте
!!CBy1/y2/y3:T?y4;      Узнаем статус посещения
!!FU&y4=1:E;            Выход, если посещен
!!CBy1/y2/y3:G0/40/30;  Ставим стражников на позиции
!!CBy1/y2/y3:G3/40/30;
!!CBy1/y2/y3:G2/40/30;
!!CBy1/y2/y3:G1/40/30;
!!CBy1/y2/y3:G4/152/1;
!!CBy1/y2/y3:G5/41/5;
!!CBy1/y2/y3:R2/0;      Удаляем руду из списка наград
!!CBy1/y2/y3:A1/?y5;    Получаем список артефактов
!!CBy1/y2/y3&y5=2:A4/0; Удаляем, если есть(если этого не делать, артефакты копятся, когда игрок посещет объект, но не идет в бой)
!!CBy1/y2/y3&y5=2:A4/1;
!!CBy1/y2/y3:R6/30000;  Золото
!!VRy5:S0 R3;           Случайное число
!!CBy1/y2/y3&y5=0:A3/12;Настраивам артефакт в зависимости от числа
!!CBy1/y2/y3&y5=1:A3/18;
!!CBy1/y2/y3&y5=2:A3/24;
!!CBy1/y2/y3&y5=3:A3/30;
!!CBy1/y2/y3:A3/133;    Также дарим стутую легиона

*Древние руины
!?OB16/11;              Триггер объекта
!!HE-1:P?y1/?y2/?y3;    Позиция на карте
!!CBy1/y2/y3:T?y4;      Узнаем статус посещения
!!FU&y4=1:E;            Выход, если посещен
!!CBy1/y2/y3:G0/211/30; Ставим стражников на позиции
!!CBy1/y2/y3:G1/232/30;
!!CBy1/y2/y3:G2/212/30;
!!CBy1/y2/y3:G3/233/30;
!!CBy1/y2/y3:R2/0;
!!CBy1/y2/y3:R1/0;
!!CBy1/y2/y3:A1/?y5;    Получаем список артефактов
!!CBy1/y2/y3&y5=2:A4/0; Удаляем, если есть(если этого не делать, артефакты копятся, когда игрок посещет объект, но не идет в бой)
!!CBy1/y2/y3&y5=2:A4/1;
!!CBy1/y2/y3&y5=2:A4/2;
!!CBy1/y2/y3:R6/10;     Золото
!!CBy1/y2/y3:A3/63;
!!CBy1/y2/y3:A3/64;
!!CBy1/y2/y3:A3/65;

*Фикс багов с озвучкой
!?SN;
!!SN:S?z10;             Узнать имя проигрываемого файла
!!VRz9:S^Rogue.wav^;
!!VRz8:S^ROGUE.WAV^;
!!FU&z10<>z9/z10<>z8:E; Выход, если не подходящий звук
!!HE-1:P?y1/?y2/?y3;
!!OBy1/y2/y3:T?y4;
!!OBy1/y2/y3:U?y5;
!!FU&y4<>16:E;
!!FU&y5<>20/y5<>12/y5<>11:E;
!!VRz10:S^DRAGOX.WAV^;  Устанавливаем звуковой файл
!!SN:Sz10;              Играем


** function to continue possibly converting a monster stack

!?FU681;
 [x1=percentile roll]
 [x2=1 if replacing some monster with WoG monsters is enabled]
 [x3=1 if replacing some L3 monsters with ghosts is enabled (Not used)]

; Реализация перенесена в DLL

**FU&x2<>1:E; [Exit if monster replacement aren't enabled]
**OB952:U?v955; [get subtype = monster type]
**FU&v955>144:E; [don't convert WoG monsters]
**FU&v955=122:E; [don't convert WoG monsters]
**FU&v955=124:E; [don't convert WoG monsters]
**FU&v955=126:E; [don't convert WoG monsters]
**FU&v955=128:E; [don't convert WoG monsters]
**MA:Lv955/?v956; [Get Monster Level: v956]
**VRv956:+1; [Add 1 to Monster Level to get 1..7: v956]
**VRx1&v957<80::2; [double chance of new monsters on small maps]

**if&v956=1:;
**FU682&x1<4:P173/0; [4% существ первого уровня становятся Санта-Гремлинами]
**FU682&x1>=5/x1<9:P192/0; [4% существ первого уровня становятся Сильванскими Кентаврами]
**FU682&x1>=10/x1<14:P246/0; [4% существ первого уровня становятся Волшебницами]
**FU682&x1>=15/x1<19:P194/0; [4% существ первого уровня становятся Оборотнями]
**FU682&x1>=20/x1<24:P225/0; [4% существ первого уровня становятся Троглодитами-Извергами]
**FU682&x1>=25/x1<29:P213/0; [4% существ первого уровня становятся Суккубами]
**en:;

**if&v956=2:;
**FU682&x1<4:P203/0; [4% существ второго уровня становятся Гномами-Лепреконами]
**FU682&x1>=5/x1<9:P208/0; [4% существ второго уровня становятся Химерами]
**FU682&x1>=10/x1<14:P224/0; [4% существ второго уровня становятся Механизированными Зомби]
**FU682&x1>=15/x1<19:P241/0; [4% существ второго уровня становятся Ящерами-гвардейцами]
**FU682&x1>=20/x1<24:P242/1; [4% существ второго уровня становятся Ящерами-лейтенантами (количество уменьшается вдвое)]

**FU682&x1>=25/x1<29:P198/0; [4% существ второго уровня становятся Элитными Арбалетчиками]
**en:;

**if&v956=3:;
**FU682&x1<4:P204/0; [4% существ третьего уровня становятся Эльфами-Сатирами]
**FU682&x1>=5/x1<9:P207/1; [4% существ третьего уровня становятся Эльфами-Фавнами (количество уменьшается вдвое)]
**FU682&x1>=10/x1<14:P159/0; [4% существ третьего уровня становятся Привидениями]
**FU682&x1>=15/x1<19:P226/0; [4% существ третьего уровня становятся Смертельными Глазами]
**FU682&x1>=20/x1<24:P234/0; [4% существ третьего уровня становятся Орками-Гладиаторами]
**FU682&x1>=25/x1<29:P248/0; [4% существ третьего уровня становятся Морскими Нимфами]
**FU682&x1>=30/x1<34:P250/1; [4% существ третьего уровня становятся Духами Океана (количество уменьшается вдвое)]

**FU682&x1>=35/x1<39:P215/0; [4% существ третьего уровня становятся Адскими Тварями]
**en:;

**if&v956=4:;
**FU682&x1<4:P235/1; [4% существ четвертого уровня становятся Повелителями Огров (количество уменьшается вдвое)]
**FU682&x1>=5/x1<9:P214/0; [4% существ четвертого уровня становятся Кошмарами]
**FU682&x1>=10/x1<14:P216/1; [4% существ четвертого уровня становятся Адскими Конями (количество уменьшается вдвое)]
**FU682&x1>=15/x1<19:P200/0; [4% существ четвертого уровня становятся Паладинами]

**FU682&x1>=20/x1<24:P249/0; [4% существ четвертого уровня становятся Плазматическими элементалями]
**FU682&x1>=25/x1<29:P221/0; [4% существ четвертого уровня становятся Графами Ужаса]

**FU682&x1>29/x1<33:P164/0; [3% of level 4's become Fire Messengers]
**FU682&x1>=33/x1<=35:P165/0; [3% of level 4's become Earth Messengers]
**FU682&x1>=36/x1<=38:P166/0; [3% of level 4's become Air Messengers]
**FU682&x1>=39/x1<=41:P167/0; [3% of level 4's become Water Messengers]
**en:;

**if&v956=5:;
**FU682&x1<4:P169/0; [4% существ пятого уровня становятся Фанатиками Войны]
**FU682&x1>=5/x1<9:P229/0; [4% существ пятого уровня становятся Легендарными Минотаврами]
**FU682&x1>=10/x1<14:P230/1; [4% существ пятого уровня становятся Проклятыми Минотаврами (количество уменьшается вдвое)]
**FU682&x1>=15/x1<19:P236/0; [4% существ пятого уровня становятся Громовыми Птероящерами]
**FU682&x1>=20/x1<24:P237/1; [4% существ пятого уровня становятся Громовыми Драконами (количество уменьшается вдвое)]

**FU682&x1>24/x1<=26:P170/0; [2% of level 5's become Arctic Sharpshooters]
**FU682&x1>=27/x1<=29:P171/0; [3% of level 5's become Lava Sharpshooters]

**FU682&x1>=30/x1<34:P205/0; [4% существ пятого уровня становятся Древними Дендроидами]
**FU682&x1>=35/x1<39:P244/1; [4% существ пятого уровня становятся Кислотными Горгонами (количество уменьшается вдвое)]
**FU682&x1>=40/x1<44:P210/0; [4% существ пятого уровня становятся Джиннами-Воинами]
**en:;

**if&v956=6:;
**FU682&x1<4:P201/0; [4% существ шестого уровня становятся Чемпионами Войны]
**FU682&x1>=5/x1<9:P168/1; [4% существ шестого уровня становятся Змеями-Горнычами (количество уменьшается вдвое)]
**FU682&x1>=10/x1<14:P223/0; [4% существ шестого уровня становятся Жнецами]
**FU682&x1>=15/x1<19:P218/0; [4% существ шестого уровня становятся Ифритами-Раджами]
**FU682&x1>=20/x1<24:P251/0; [4% существ шестого уровня становятся Астральными Элементалями]

**FU682&x1>=25/x1<29:P238/0; [4% существ шестого уровня становятся Циклопами-Героями]

*!FU682&x1>=24/x1<27:P193/0; [3% of level 6's become Sorceresses]
**en:;

*** Конец скрипта ***
