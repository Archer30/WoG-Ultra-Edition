ZVSE
ERMS_ScriptDate=24.8(August).2004
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2004.6.29.918

**Настройка карт и изменения в механиках ИИ

!#VRv6777:S0;
!#VRv6775:S0;
!#VRv6900:S1000;                 Бонусы по умолчанию включены
!?FU15045;
!!IF:Q3/z156500;                 Задаем вопрос, хочет ли игрок настроить что-либо
!!IF&-3:Q2/z156501;              Если не хочет, спрашиваем о дополнительных особенностях карты
!!FU&-2/-3:E;                    Выход, если отказался
!!FU22001&-3:P;                  Настройка карты
!!FU&-3:E;
!!VRv6900:S0;                    Обнуляем все статусы
!!VRv6901:S0;
!!VRv6902:S0;
!!VRv6771:S0;
!!VRv6770:S0;
!!IF:Q1/z156502;                 Хотите включить базовое усиление ИИ?
!!VRv6900&1:+1000;
!!IF&1:Q2/z156503;               Хотите максимально усилить ИИ?
!!VRv6901&1/2:+1000;
!!IF&v6775<>1000:Q1/z156504;     Хотите включить режим эксперта?
!!VRv6775&1/v6775<>1000:+1000;

!!IF&v6777<>1000:Q1/z156511;
!!VRv6777&1/v6777<>1000:+1000;

!!IF:Q1/z156505;                 Включить корректировку ИИ?
!!VRv6902&1:+1000;

!!IF:Q1/z156506;                 Разрешить автоматическое улучшение существ для ИИ?
!!VRv6771&1:+1;

!!IF&1:Q1/z156507;               Разрешить автоматическое улучшение существ 7 уровня для ИИ?
!!VRv6770&1:+1;

!!IF:Q1/z156512;                 Хотите разрешить ИИ сохранять часть армии при побеге?
!!SN&1:W^AISavesArmyEnabled^/1;
!!SN&-1:W^AISavesArmyEnabled^/0;

!!IF:Q2/z156501;                 Настроить опыт существ?
!!FU&-2:E;
!!FU22001:P;

* Полное открытие карты
!?FU(ShowEverything);
!!OW:Ix16/?y1; Статус ИИ у игрока
!!FU&y1=0:E; Выход, если не ИИ
!!OW:Wx16/?v1; Кол-во городов у игрока
!!if&v6775=1000|v1=0:; Если включена опция открытия карты или нет городов
 !!UN:X?y2/?y3; Получаем размер карты
 !!VRy4:Sy2;
 !!VRy4::2;
 !!VRy2:+10;
 !!UN:Sy4/y4/0/x16/y2; Открываем карту
 !!UN&y3=1:Sy4/y4/1/x16/y2; Открываем подземелье, если оно есть
!!en:;

*?FU(OnGameEnter);
**FU:E;
**FC10:B1/7;
**FC0:R4/1000;
**FC0:T?y1;
**MA:R5/5;
**MA:W5/5;
**FC7:C1/?y1/?y2/?y3;
**IF:M^%Y1 %Y2 %Y3 ^;
**FC8:S8;
**FC8:M0/2;
**FC8:M1/100;
**MA:R130/10 R131/10;
**FC8:B0/84 H2/6/6 H4/0/130/131;

*?OB62;
**OB998:C?y1;
**IF:M^%Y1 1^;
**VRy2:S20;
**PRv998/v999/v1000:Hy2;
**IF:M^%V1^;
**UN:C6918840/4/?y1;
**VRy1:+92;
**UN:Cy1/4/?y2;
**SN:E4228816/2/y2/v998/v999/v1000;
**UN:Cv1/24/2/?v2;
**UN:Cv1/1/?v1;
**IF:M^%V1^;
**FC2:M0/2;
**FC2:S2;

*?OB113;
**OB998:C?y1;
**IF:M^%Y1^;
**UN:C6918840/4/?y1;
**VRy1:+92;
**UN:Cy1/4/?y2;
**SN:E4228816/2/y2/v998/v999/v1000;
**UN:Cv1/24/2/?v2;
**UN:Cv1/1/?v1;
**IF:M^%V1^;

*?OB57;
**OB998:C?y1;
**IF:M^%Y1^;
**UN:C6918840/4/?y1;
**VRy1:+92;
**UN:Cy1/4/?y2;
**SN:E4228816/2/y2/v998/v999/v1000;
**UN:Cv1/24/2/?v2;
**IF:M^%V2^;
**UN:Cv1/1/?v1;
**IF:M^%V1^;

*Новые особенности карт
!#VRv6903:C4;                По умолчанию разрешаем стройку
!?CM5;                       Код срабатывает при нажатии Alt и левой кнопки мыши на миникарте
!!CM:I?v1;
!!CM:F?v2;
!!FU&v1<>1|v2<>32:E;
!!CM:R0;
!!FU15045:P;
!!IF:Q1/z125218;
!!FU&-1:E;

!!IF:V1/0;
!!IF:V2/0;
!!IF&v6903=4:Q1/z125221;
!!IF&v6903=5:Q2/z125222;
!!VRv6903&1/v6903=4:S5;      Изменям действие
!!VRv6903&2/v6903=5:S4;
!!DO15049/0/v6785/1&1:P;     Функция устанавливает результат во всех городах
!!DO15049/0/v6785/1&2:P;

!!IF:V1/0;
!!IF:V2/0;
!!IF&v6776=0:Q1/z125220;
!!IF&v6776=200:Q2/z125219;
!!VRv6776&1/v6776=0:S200;
!!VRv6776&2/v6776=200:S0;
*!IF:G1/3/0/156011/1/;
!!IF:V1/0;
!!IF:V2/0;
!!UN:P240/?v1;
!!IF&v1=1:Q1/z125232;
!!IF&v1=0:Q2/z125233;
!!SN&2:W^PeasRobbOptionEnabled^/1;
!!SN&1:W^PeasRobbOptionEnabled^/0;

!?FU15049;
!!CA0/x16:Bv6903/37;
!!CA0/x16:Bv6903/38;
!!CA0/x16:Bv6903/39;
!!CA0/x16:Bv6903/40;
!!CA0/x16:Bv6903/41;
!!CA0/x16:Bv6903/42;
!!CA0/x16:Bv6903/43;

!?PI;
!!UN:X?i/?j;
!!VRv6900&i<72:S0;           На маленьких картах бонус изначально выключен, чтобы ИИ не был слишком жестким


!?PI;                        Вопрос о появлении новых существ

!!UN:P139/0;                 Фишка больше не актуальна, так как пропатчен сам генератор


!!UN:P139/?y1;
!!FU&y1=0:E;
; thanks to feanor
!!UN:C6919480/4/?y10;
!!VRy11:Sy10 +128980;
!!UN:Cy11/1/?y12;
!!UN&y12=114:P139/0;         Отмена, если случайная карта
!!FU&y12=114:E;
!!IF:Q1/z125231;
!!UN&-1:P139/0;

*Коректировка поведения ИИ
!#UN:J4/35000;               Увеличиваем компу радиус "раздумий"

*Постройка дополнительных строений
!?OB98;                      Триггер города
!!FU&1000:E;                 Выход, если человек
!!HE-1:P?y50/?y51/?y52;
!!OBy50/y51/y52:T?y60;
!!FU&y60<>98:E;              Выход, если была осада

!!CAy50/y51/y52:T?y80;       Проверяем тип города (y80)

!!CAy50/y51/y52:B3/21;       Проверяем доп. строения
!!VRy40&1:S1;
!!VRy40&-1:S0;
!!CAy50/y51/y52:B3/17;       Проверяем
!!VRy41&1:S1;
!!VRy41&-1:S0;
!!CAy50/y51/y52:B3/22;       Проверяем
!!VRy42&1:S1;
!!VRy42&-1:S0;
!!VRy42&y80=4:S1;            Некрополис - исключение
!!CAy50/y51/y52:B3/23;       Проверяем
!!VRy43&1:S1;
!!VRy43&-1:S0;
!!FU&v6902=0/y40=1/y42=1/y41=1/y43=1:E;
!!CAy50/y51/y52:B3/7;        Проверяем форт
!!VRy44&1:S1;
!!VRy44&-1:S0;
!!CAy50/y51/y52:B3/8;        Проверяем цитадель
!!VRy45&1:S1;
!!VRy45&-1:S0;
!!CAy50/y51/y52:B3/9;        Проверяем замок
!!VRy46&1:S1;
!!VRy46&-1:S0;
!!FU&v6902=1000/y46=1/y40=1/y42=1/y41=1/y43=1:E;

!!CAy50/y51/y52:B3/0;        Проверяем гм1 (y47)
!!VRy47&1:S1;
!!VRy47&-1:S0;
!!CAy50/y51/y52:B3/1;        Проверяем гм2
!!VRy47&1:S1;
!!CAy50/y51/y52:B3/2;        Проверяем гм3
!!VRy47&1:S1;
!!CAy50/y51/y52:B3/3;        Проверяем гм4
!!VRy47&1:S1;
!!CAy50/y51/y52:B3/4;        Проверяем гм5
!!VRy47&1:S1;
!!CAy50/y51/y52:B3/14;       Проверяем рынок (y48)
!!VRy48&1:S1;
!!VRy48&-1:S0;
!!CAy50/y51/y52:B3/15;       Проверяем хранилище
!!VRy48&1:S1;
!!CAy50/y51/y52:B3/16;       Проверяем кузницу (y49)
!!VRy49&1:S1;
!!VRy49&-1:S0;
!!CAy50/y51/y52:B3/5;        Проверяем таверну (y60)
!!VRy60&1:S1;
!!VRy60&-1:S0;
!!CAy50/y51/y52:B3/18;       Проверяем гильдию горняков (y61)
!!VRy61&1:S1;
!!VRy61&-1:S0;
!!CAy50/y51/y52:B3/19;
!!VRy61&1:S1;

!!IF:V10/0 V9/0;             Статус постройки

!!IF:V1/0;                   Статус необходимости форта (21)
!!IF&y80>1/y80<4:V1/1;       2,3,7
!!IF&y80=7:V1/1;
!!IF:V2/0;                   Статус необходимости гм (21)
!!IF&y80>3/y80<6:V2/1;       4,5,8
!!IF&y80=8:V2/1;

!!VRy70:S0;                  Статус наличия форта (y70)
!!VRy70&v6902=1000:S1;
!!VRy70&y44=1|y45=1:S1;
!!VRy70&y46=1:S1;

!!CAy50/y51/y52&v6902=1000/y44=0/y45=0/y46=0:B6/7; Строим форт, если включена корректировка
!!if&y40=0:;
 !!CAy50/y51/y52&y70=1/1:B6/21;                    Строим, если здания нет, есть форт и тип города подходит
 !!CAy50/y51/y52&y47=1/2:B6/21;                    Строим, если здания нет, есть гм и тип города подходит
 !!IF&y70=1/1:V10/1;
 !!IF&y47=1/2:V10/1;
!!en:;
!!FU&10:E;                                         Выходим, если построили

!!IF&v6902=1000/y44=0/y45=0/y46=0:V9/1;            Отмечаем постройку форта

!!if&y40=0:;
 !!CAy50/y51/y52&y80=0/y49=1:B6/21;                Строим конюшню
 !!CAy50/y51/y52&y41=1/y80=1:B6/21;                Строим фонтан удачи, если здания нет и есть таинственный пруд
 !!CAy50/y51/y52&y48=1/y80=6:B6/21;                Строим гильдию наемников, если есть рынок
 !!IF&y41=1/y80=1:V10/1;
 !!IF&y48=1/y80=6:V10/1;
 !!IF&y80=0/y49=1:V10/1;
!!en:;
!!FU&10:E;                                         Выходим, если построили

!!CAy50/y51/y52&v6902=1000/y45=0/y46=0/-9:B6/8;    Строим цитадель, если включена корректировка
!!if&y41=0/y80=1:;
 !!CAy50/y51/y52:B6/17;                            Строим таинственный пруд
 !!IF:V10/1;
!!en:;
!!FU&10:E;

!!IF:V1/0;                                         Статус необходимости форта (17)
!!IF&y80>3/y80<8/y80<>5:V1/1;                      4,6,7
!!IF:V2/0;                                         Статус необходимости рынка (17)
!!IF&y80=2:V2/1;                                   2,5,8
!!IF&y80=5:V2/1;
!!IF&y80=8:V2/1;

!!if&y41=0:;
 !!CAy50/y51/y52&y70=1/1:B6/17;                    Строим, если здания нет, есть форт и тип города подходит
 !!CAy50/y51/y52&y48=1/2:B6/17;                    Строим, если здания нет, есть рынок и тип города подходит
 !!IF&y70=1/1:V10/1;
 !!IF&y48=1/2:V10/1;
!!en:;
!!FU&10:E;                                         Выходим, если построили

!!IF&v6902=1000/y44=0/y45=0:V9/1;                  Отмечаем постройку цитадели

!!CAy50/y51/y52&v6902=1000/y46=0/-9:B6/9;          Строим замок, если включена корректировка
!!if&y43=0:;
 !!CAy50/y51/y52&y80=5:B6/23;                      Строим академию
 !!CAy50/y51/y52&y80>1/y80<4/y47=1:B6/23;          Строим стену знаний и орден огня
 !!CAy50/y51/y52&y80=6/y70=1:B6/23;                Строим Храм Валгаллы
 !!IF&y80=5:V10/1;
 !!IF&y80>1/y80<4/y47=1:V10/1;
 !!IF&y80=6/y70=1:V10/1;
!!en:;
!!FU&10:E;                                         Выходим, если построили

!!if&y42=0:;
 !!CAy50/y51/y52&y80=0/y60=1:B6/22;                Строим братсво меча
 !!CAy50/y51/y52&y80=1/y61=1:B6/22;                Строим сокровищницу
 !!CAy50/y51/y52&y80=2/y47=1:B6/22;                Строим библиотеку
 !!CAy50/y51/y52&y80=5:B6/22;                      Строим портал вызова
 !!CAy50/y51/y52&y80=6/y49=1:B6/22;                Строим двор баллист
 !!CAy50/y51/y52&y80=7/y40=1:B6/22;                Строим обелиск крови
!!en:;
!!CAy50/y51/y52:B3/8;                              Проверяем цитадель
!!VRy45&1:S1;
!!CAy50/y51/y52:B3/9;                              Проверяем замок
!!VRy45&1:S1;
!!CAy50/y51/y52&y42=0/y80=3/y45=1:B6/22;           Строим врата замка
*ИИ не строит трансформатор ради собственной безопасности :)

!#TM4:S1/999/1/255;     ИИ получает дополнительные деньги
!?TM4;
!!OW:C?y5;
!!OW:Iy5/?y6/?y7;
!!FU&y6=0|y7=1:E;
!!DO(ShowEverything)/0/7/1:P;                     Открытие карты
!!OW:Ry5/6/?y6;
!!VRy30:Sc;
!!VRy6:+1000;
!!VRy6&y30>28:+2000;
!!VRy6&y30>50:+5000;
!!VRy6&v6779=1:+3000;
!!VRy6&v6780=1:+4000;
!!OW:Ry5/6/y6;

!!FU&v6777=0:E;                                   Бесконечные ресурсы
!!OW:Ry5/0/99999;
!!OW:Ry5/1/99999;
!!OW:Ry5/2/99999;
!!OW:Ry5/3/99999;
!!OW:Ry5/4/99999;
!!OW:Ry5/5/99999;
!!OW:Ry5/6/9899999;

!?OB98;                 После входа в город
!!FU&v6900=0|1000:E;
!!HE-1:Y5/1/2/1;        ИИ получает дополнительные ресурсы
!!HE-1:Y9/70/10/1;      ИИ получает дополнительный опыт

!?OB54;                 После боя с монстром
!!FU&1000:E;
!!HE-1&v6901=1000:Y5/1/1/1;ИИ получает дополнительные ресурсы
!!HE-1:Y9/150/2/1;      ИИ получает дополнительный опыт

!?BA1;                  Снятие бонусов при поражении
!!BA:O?y1/?y2;
!!BA&y1=-1:H0/?y3;
!!HEy3&y1=-1:Y9/0/0/0;
!!VRy4:S-2;
!!BA&y2=-1:H1/?y4;
!!FU&y4<0:E;
!!HEy4:Y9/0/0/0;

!?HL-1;                 ИИ получает больше навыков
!!FU&1000:E;
!!VRy14:Sc;
!!FU&y14<14:E;
!!HE-1:F?v2/?v3/?v4/?v5;
!!VRy1:S2 R3;
!!VRvy1:+1;
!!VRy1&v6901=1000:S2 R3;
!!VRvy1&v6901=1000:R1;
!!HE-1:Fv2/v3/v4/v5;

*ИИ маскируется
!?FU(HideAI);
; x1 - номер героя [0...155]
; x2 - маскировка:
;         0=нет навыка воздуха
;         1=базовый навык
;         2=продв. навык
;         3=эксп. навык
;         -1=снять маскировку
!!UN:C6919480/4/?y1;
!!VRy2:Sx1 *1170 +136736 +y1 +270; 270=контроль маскировки
!!UN:Cy2/4/x2;

!?FU(OnEveryDay);
!!OW:I-1/?i;
!!FU&i=0:E;
!!OW:O-1/?v1/?v2/?v3/?v4/?v5/?v6/?v7/?v8/?v9;
!!FU&v1=0:E;
!!VRv1:+1;
!!re y1/2/v1:;
 !!HEvy1:M4/?y2;
 !!co&y2=0;
 !!VRn:S0 R2;
 !!co&n<>2;
 !!HEvy1:S15/?y3;
 !!FU(HideAI):Pvy1/y3;
!!en:;

*Возрождение командиров ИИ
!?BA1;
!!UN:P3/?y1;
!!if&y1=0:;
!!BA:O?y5/?y6;
!!OW:Iy6/?y7;
!!OW:Iy5/?y8;
!!FU&y7=0/y8=0:E;       Выйти, если не ИИ

!!BA:H0/?y9;
!!BA:H1/?y10;

!!COy9&y8=1:D0;
!!COy10&y7=1/y10<>-2:D0;
!!en:;

** Коректировка боевого ИИ
*?BA0;
**VRv6795:S0;           Информация о бое
**VRv6794:S0;           Информация о баррикадах
**SN:W^_DeadStackNumber^/-1;
*?BF;                   Перед битвой
**BA:S?y2;              Получить тип осадной битвы
**FU&y2<2:E;            Выйти, если не осада города
**FU&y2>3:E;
**BA:O?y5/?y6;
**OW:Iy6/?y7;
**BA:Q?y8;
**FU&y7=0/y8=0:E;       Выйти, если не ИИ
**BA:H0/?y9;
**BA:H1/?y10;
**HEy10&y10>-1:I?y11/1;
**VRy11&y10=-2:S0;
**HEy9:I?y12/1;
**VRy12:-y11;
**FU&y12>200/y11<100:E;
**VRy13:S0;
**VRy14:S0;
**re i/0/41:;
 **BMi:H?j;
 **BMi:N?k;
 **BMi:L?l;
 **VRj:*k -l;
 **VRy13&i<21:+j;
 **VRy14&i>20:+j;
**en:;
**VRy15&y13>=y14:Sy13 -y14;
**VRy15&y13<y14:Sy14 -y13;
**VRe1&y13>=y14:Sy13 :y14;
**VRe1&y13<y14:Sy14 :y13;
**VRe1:*100;
**FU&e1>=140/y14>y13:E;
**VRv6795:S1;

*?BG0&v6794=0/v6795=1;  Только если не подходит бой
**HE-10:N?m;
**IF:Wm;
**HE-10:A2/5/?w19/?n;
**BU:S148/1/94/0/-1/0;  Если нет баррикад, ставим тележку
**BU:E94/?y2;           Получаем номер стека
**BMy2:K1000;           Уничтожаем
**SN:W^_DeadStackNumber^/y2;
**BA:Q?y8;              Если не быстрая битва
**BU&y8=0:R;            Обновляем поле боя
**VRz-1:Sz156508;
**BU:Mz-1;              Сообщение
**VRv6794:+1;

*?BR&v997>0;
**SN:W^_DeadStackNumber^/?y1;
**IF:M^%Y1^;
**BMy1:Fd16 Fd-64;
**BM0:C38/94/3/3/1;
**BMy1:Pd-1;
**BU:R;
**IF:M^%Y4 %Y5 %Y4^;

*?BA1;
**HE-10:N?m;
**IF:Wm;
**HE-10&w19>0/v6794>0:A4/5;

*Побег с сохранением армии для ИИ
!#SN:W^AISavesArmyEnabled^/0;
!?BA0;
!!SN:W^AIRetreats^/-10;
!!SN:W^AISide^/-10;

!?BG0;
!!BG:A?y1;
!!FU&y1<>4:E;
!!BG:Q?y2;
!!BA:O?y3/?y4;
!!OW&y2=0:Iy3/?y5;
!!OW&y2=1:Iy4/?y5;
!!FU&y5=0:E;
!!BA:Hy2/?y6;
!!SN:W^AIRetreats^/y6;
!!SN:W^AISide^/y2;

!?BA1;
!!SN:W^AISavesArmyEnabled^/?n;
!!FU&n=0:E;
!!SN:W^AIRetreats^/?y1;
!!FU&y1<0:E;
!!SN:W^AISide^/?y2;

!!DO(GiveTroops)/0/18/1&y2=0:Py1;
!!DO(GiveTroops)/21/39/1&y2=1:Py1;

!?FU(GiveTroops);
!!BMx16:T?y1 N?y2 O?y3; [Тип (y1), Колисчество (y2), Слот (y3) стека x16]
!!FU|y1<0/y2<1/y3<0:E; [Выход, если слот пуст]
!!VRy4:Sy2 %2; [Вычисляем остаток для округления вверх]
!!VRy5:Sy2 :2 +y4; [50% существ: y5]
!!HEx1:C0/y3/y1/y5; [Возвращаем 50% от количества существ в стеке]

*Возможность отмены хода баллисты
!?BG0;
!!BG:N?y91;
!!BMy91:T?y92;
!!FU&y92<>146:E;
!!BG:E?y93;
!!FU&y93=-1:E;
!!IF:V5/0;
!!BMy93:G62/?y11/d0;
!!IF&y11>0:V5/1;        Eсли существо ослеплено
!!BMy93:G70/?y12/d0;
!!IF&y12>0:V5/1;        Eсли существо окаменело
!!BMy93:G74/?y13/d0;
!!IF&y13>0:V5/1;        Eсли существо парализовано
!!FU&-5:E;

!!BA:O?y94/?y95;
!!OW:Iy94/?y96;         Проверка на ИИ
!!FU&y91<21/y96=1:E;
!!OW:Iy95/?y97;
!!FU&y91>20/y97=1:E;
!!IF:Q6/z156509;
!!FU&-6:E;
!!BG:A12;
!!VRz-8:Sz156510;
!!BU:Mz-8;              Сообщение

*Настройка поведения стрелковых башен
!?BG0;                  Триггер
!!BA:S?y2;              Получить тип осадной битвы
!!FU&y2<2:E;            Выход, если не осада города
!!BG:N?y1;
!!BMy1:T?y2;
!!FU15050&y2=149:P;     Если сейчас ход башни, вызываем функцию, обрабатывающую ее поведение

!?FU15050;
!!BA:H1/?h;             Узнаем героя-защитника
!!BA:O?y1/?k;           Цвет защитника
!!OW:Ik/?j;             Проверяем статус ИИ
!!VRl:S0;
!!HEh&h>-1:S20/?l;      Проверяем артилерию
!!BA:Q?o;               Проверяем быструю битву
!!FU&j=0/l>0/o=0:E;     Выход, если это человек, и он может сам управлять башнями
!!COh&h>-1:E?y2;
!!if&y2=1:;             Если командиры разрешены
 !!COh&h>-1:T?y3;       Получаем тип командира
 !!COh&h>-1:D?y4;       Получаем тип командира
 !!FU&y2=6/y3=0:E;      Выход, если варварский (и живой)
!!en:;
!!VRv9:S-1;             Номер стека
!!VRv10:S-10;           Его "опасность"

!!DO15051/0/19/1:P35/20;Определяем лучшую цель
!!VRv6772&v6772=0:S1;   Отмечаем ход центральной башни
!!FU&v9=-1:E;           Если цель не выбрана, ИИ делает это сам на свое усмотрение
!!BMv9:P?v2;            Позиция цели
!!BG:Dv2;
!!BG:A7;                Выстрел

!?FU15051;              Функция выбора цели
!!BMx16:T?v5;           Получаем тип существа
!!FU&v5<0:E;            Выход, если нет существа

!!BMx16:F?i;            Сохраняем флаги стека в i
!!VRy80:Si &64;
!!FU&y80>0:E;           Выход, если это боевая машина

!!BMx16:G62/?y11/d0;
!!FU&y11>0:E;           Выход, если существо ослеплено
!!BMx16:G70/?y12/d0;
!!FU&y12>0:E;           Выход, если существо окаменело
!!BMx16:G74/?y13/d0;
!!FU&y13>0:E;           Выход, если существо парализовано

!!VRy80:Si &2097152;
!!FU&y80>0:E;           Выход, если существо умерло

!!BMx16:N?v3;
!!MA:Pv5/?v6;           Получаем здоровье
!!VRy1:Sv3 *v6;
!!BMx16:L?v4;
!!VRy1:-v4;

!!VRy2:S0;              Счетчик опасности

!!BMx16:P?v4;
!!FU&v4>186:E;          Выход, если ошибка
!!VRy3:Sv4 +1 %17;
!!VRy2&y3>12:+3;        Если существо в стенах
!!if&y3<13:;
 !!VRy2&v4=63:+3;       Если существо в стенах
 !!VRy2&v4=79:+3;       Если существо в стенах
 !!VRy2&v4=97:+3;       Если существо в стенах
 !!VRy2&v4=113:+3;      Если существо в стенах
 !!VRy2&v4=131:+3;      Если существо в стенах
!!en:;

!!VRy4:Si &2;
!!VRy2&y4>0/y3<13/1:+2; Если существо умеет летать

!!VRy4:Si &65536;
!!VRy2&y4>0:+1;         Если существу не могут сдавать сдачу

!!BMx16:U3/?v7;
!!VRy4:Si &4;
!!VRy2&v7>0/y4>0:+2;    Если есть снаряды (существо может стрелять)

!!VRy2&v6772<>0/y1<x1/y1>x2:+1;

!!VRy2&v6>150:-2;       Если существо трудно убить
!!VRy2&v6>450:-1;       Если существо трудно убить

!!UN:C6919200/4/?y10;   Менеджер битвы
!!UN:Cy10/21692/4/?y11; Получаем количество стеков у атакующего
!!if&y11>4:;            Если больше четырех
 !!BU:E120/?y12;        Узнаем, жива ли Катапульта
 !!VRy11&y12>-1:-1;     Не считаем ее стеком
 !!BU:E154/?y13;        Узнаем, жива ли Палатка
 !!VRy11&y13>-1:-1;     Не считаем ее стеком
 !!BU:E52/?y14;         Узнаем, жива ли Баллиста
 !!VRy11&y14>-1:-1;     Не считаем ее стеком
 !!BU:E18/?y15;         Узнаем, жива ли Повозка
 !!VRy11&y15>-1:-1;     Не считаем ее стеком

 !!if&v6<x1:;           Если количество стеков все еще больше четырех
  !!VRy2&v6772<>0/y1<x2/y11>4:-5;  Если цель слишком слаба
  !!VRy2&v6772=0/y1<x1:-5;   Уменьшаем приоритет
 !!en:;
!!en:;

!!VRv9&y2>v10:Sx16;
!!BMx16&y2=v10:U1/?y14; Дополнительное сравнение по урону
!!BMv9&y2=v10:U1/?y15;
!!VRv9&y2=v10/y15<y14:Sx16;
!!VRv10&y2>v10:Sy2;
*!IF:M^%V10 %Y2 %V7 Type - %V5 MHelth - %V6 CHelth - %Y1 Position - %Y3 St.n - %X16^;    Отладочная информация

!?BG0;                  Отмена ненужной защиты существа
!!BG:A?y50;             Тип действия
!!FU&y50=1:E;           Магия не в счет
!!BG:N?y51;             Подсчитываем количество действий в раунде
!!BMy51:T?y52;          Тип сушества
!!FU&y52=149:E;         Башни не в счет
!!FU&y52=145:E;         Катапульта не в счет
!!VRv6773:+1;           Увеличиваем индикатор
!!BA:S?y53;             Получить тип осадной битвы
!!FU&y53<2:E;           Выйти, если не осада города
!!FU15052&y52=2:Py50;   Если ходьба, вызываем функцию
!!FU&v6773>1:E;         Выйти, если это не первое существо
!!FU&y52<>3:E;          Выход, если не защита
!!BMy51:I?y61;
!!FU&y61=0:E;           Выйти, если это нападающий
!!BA:O?y99/?y54;        Цвет защитника
!!OW:Iy54/?y55;         Проверяем статус ИИ
!!BA:Q?y56;             Проверяем быструю битву
!!FU&y55=0/y56=0:E;     Выход, если это человек
!!BG:A8;                Ждем

!?FU15052;              Запрет выходить из города
!!VRy50:Sx1;
!!BMy50:I?y61;
!!FU&y61=0:E;           Выйти, если это нападающий
!!BA:P?v2/?v3/?v4;      В v2 v3 v4 координаты на карте
!!VRv3:-1;              Смещение на шаг вверх для получения позиции города, а не героя
!!BA:O?y99/?y54;        Цвет защитника
!!OW:Iy54/?y55;         Проверяем статус ИИ
!!BA:Q?y56;             Проверяем быструю битву
!!FU&y55=0/y56=0:E;     Выход, если это человек
!!OBv2/v3/v4:U?v5;      В v5 получить подтип объета (тип города)
!!OBv2/v3/v4:T?v6;      На всякий случай проверка на тип объета
!!FU&v6=98/v5=2:E;      Выходим, если Башня
!!BG:D?y30;             Получаем конечную позицию
!!VRy66:S8;

!!BMy50:F?y67 G60/?y68/?y69;
!!FU&y68>0:E;
!!VRy67:&1;
**IF:M^%Y67^;
**VRy67:&33554432;
**VRy66&y67>0:S3;
**IF:M^%Y67^;
**BMy50&y67=0:Fd33554432;
**BMy50:F?y67;
**IF:M^%Y67^;

!!FU&v997<0:E;          Выход, если тактическая фаза

!!BG&y30=182:Ay66;      Ждем
!!BG&y30=130:Ay66;      Ждем
!!BG&y30=78:Ay66;       Ждем
!!BG&y30=29:Ay66;       Ждем

!!BG&y30=181:Ay66;      Ждем
!!BG&y30=129:Ay66;      Ждем
!!BG&y30=146:Ay66;      Ждем
!!BG&y30=11:Ay66;       Ждем
!!BG&y30=28:Ay66;       Ждем
!!BG&y30=77:Ay66;       Ждем
!!BG&y30=61:Ay66;       Ждем

!!FU&y67=0:E;           Выход, если существо одноклеточное
!!BG&y30=183:Ay66;      Ждем
!!BG&y30=131:Ay66;      Ждем
!!BG&y30=79:Ay66;       Ждем
!!BG&y30=30:Ay66;       Ждем

!?BR;
!!VRv6772:S0;           Обновление статуса центральной башни
!!VRv6773:S0;           Обновление счетчика действий

*Контроль маны
!?BF;
!!BA:H0/?y1;            Атакующий
!!BA:H1/?y2;            Защитник
!!BA:O?v1/?v2;          Хозяева
!!OW:Iv1/?y3;           Проверяем статус ИИ

!!if&y3=1:;             Если ИИ
 !!HEy1:F?n/?n/?n/?y4;  Получаем знания
 !!VRy4:*4;
 !!HEy1:I?y5/1;         Получаем актуальное количество очков
 !!HEy1&y5<y4:Iy4/1;    Добавляем ману, если ее меньше критической нормы (40%)
!!en:;

!!FU&y2=-2:E;           Выход, если нет защитника
!!OW:Iv2/?y6;           Проверяем статус ИИ
!!FU&y6=0:E;            Выход, если не ИИ

!!HEy2:F?n/?n/?n/?y7;   Получаем знания
!!VRy7:*4;
!!HEy2:I?y8/1;          Получаем актуальное количество очков
!!HEy2&y8<y7:Iy7/1;     Добавляем ману, если ее меньше критической нормы (40%)


*Отмена хода в ров
*?BA0;
**VRp:S-1;

**FU&y2=149:E;          Выходим после хода башни
**BG:A?y3;
**FU&y3<>2:E;
**BA:O?y4/?y5;
**OW:Iy5/?y6;
**FU&y6=0:E;

**BG:D?y7;
**BG&v997=-1/y7=181:A0;
**BU&p=y1:T0;
**VRp&v997=-1/y7=181/p=-1:Sy1;
**FU&v997=-1:E;


**181           позиции
**164
**146
**129
**111
**77
**61
**44
**28
**11

**BMy1&y4=181:


** Author orig.  : Sergroj
** Name          : AI behavior correction
** Name rus.     : Исправление поведения ИИ

!?BA0;
!!BA: A=1;
!!FU&1: E;
!!BA: S=0;
!!FU&1: E;
!!BA: Od/?y-1;
!!OW&y-1>=0: Iy-1/=1;
!!VRy-1&1: S-1;
!!FU&y-1>=0: E;
!!MA: I146/200; Ballista
!!MA: I147/50;  First Aid Tent

!!BA:S?y2;      получить тип осадной битвы
!!FU&y2>1:E;    выйти если осада города
!!MA: I148/150; Ammo Cart

!?BA1; [Restore attributes]
!!MA: I146/600; Ballista
!!MA: I147/300; First Aid Tent
!!MA: I148/400; Ammo Cart

** end

Author PerryR + owner check by Raistlin


AI gets 50% of combat loses recovered + 50% of used mana points

!?FU(OnBeforeBattleUniversal)&-1000;

!!BA:O?n/?v1;
!!FU&v1<>-1:E;                          Only after fight with newtrals!

!!BA:H0/?y1;                            [Attacker Hero Number]
!!FU&y1<0:E;                            [Exit if no Hero]

!!HEy1:O?y2;                            [get owner of this hero]
!!OW:Iy2/?y3;                           [Check if AI (1)]
!!FU|y2=-1/y3=0:E;                      [Exit if no owner or is from Human]

!!SN:W^AI_Hero_Attacker_Owner^/y2;      [Save Owner Before Fight]

!!HEy1:Z?y10;                           Получаем структуру героя.
!!UN:Cy10/24/2/?y11;                    Получаем очки магии (HE:I - очень сильно тормозит).
!!SN:W^AI_Hero_Mana_^/y11;              [Сохраняем количество очков магии]

!!re i/0/6:;                            [cycle hero troops]
 !!HEy1:C0/i/?y2/?y3; !!SN:W^ACM_AI_Slot_Type_%Vi^/y2; !!SN:W^ACM_AI_Slot_Number_%Vi^/y3; [save slots]
!!en:;


!?FU(OnAfterBattleUniversal)&-1000;

!!BA:O?n/?v1;
!!FU&v1<>-1:E;                          Only after fight with newtrals

!!BA:H0/?y1;                            [Attacker Hero Number]
!!FU&y1<0:E;                            [Exit if no Hero]
!!HEy1:O?y2;                            [get owner of this hero]
!!SN:W^AI_Hero_Attacker_Owner^/?y3;     [Check Owner Before Fight]
!!FU&y2<>y3:E;                          [Prevent to give Bonus if retreat]
!!FU&y2=-1:E;                           [Exit if no owner]


!!HEy1:Z?y10;                           Получаем структуру героя.
!!UN:Cy10/24/2/?y11;                    Получаем очки магии (HE:I - очень сильно тормозит).
!!SN:W^AI_Hero_Mana_^/?y12;             Получаем старое количество очков магии.
!!if&y11<y12:;                          Если герой потратил ману.
 !!VRy12:-y11 /2;                       Находим половину от потраченных очков.
 !!VRy12&y12<1:S1;                      Минимум 1.
 !!UN:Cy10/24/2/dy12;                   Добавляем ману обратно.
!!en:;


!!re i/0/6:;                            [cycle hero troops]
  !!HEy1:C0/i/?y2/?y3; !!SN:W^ACM_AI_Slot_Type_%Vi^/?y20; !!SN:W^ACM_AI_Slot_Number_%Vi^/?y30; [compare with old value]
  !!VRy5:Sy30-y3:2+1;                   [Recovers 50% of losses rounded up]
  !!HEy1&y2=y20/y3<y30/y2<>1/y5>0:C0/i/?y2/dy5; [add troops to AI again]
!!en:;

*********************************The END****************************************


; ИИ оставляет охрану на шахтах, которые украл у человека.
; Спасибо Bes за идею!


!?OB53&-1000; Перед посещением шахты героем ИИ
!!MNv998/v999/v1000:O?y1; Получаем ее владельца
!!FU&y1=-1:E; Выход, если шахта никому не принадлежит
!!OW:Iy1/?y2; Статус ИИ у владельца шахты
!!FU&y2=1:E; Выход, если не человек

!!SN:W^MineOwnerIsHuman^/1; Отмечаем, что в настоящий момент обрабатывается шахта человека


!$OB53&-1000; После посещения шахты героем ИИ
!!SN:W^MineOwnerIsHuman^/?y1; Узнаем, обрабатывается ли сейчас бывшая шахта человека
!!FU&y1<>1:E; Выход, если не обрабатывается

!!SN:W^MineOwnerIsHuman^/1; Отмечаем, что шахта обработана

!!MNv998/v999/v1000:O?y2; Получаем цвет владельца
!!OW:C?y3; Получаем цвет текущего игрока
!!FU&y2<>y3:E; Выход, если цвета не совпадают (например, ИИ не смог пробить охрану)

!!re i/0/6:; Проходимся по слотам армии героя
 !!HE-1:C0/i/?y4/?y5; Тип и количество существ
 !!if&y5>80:; Если больше чем 80
  !!MA:Ly4/?y6; Смотрим уровень существа
  !!if&y6=0:; Если это существо первого уровня
   !!VRy7:S7; Изначальное количество, сколько существ будет оставлено
   !!SN:W^StrongLvl1Cr%Y6^/?y8; Смотрим, не нужно ли изменить количество оставляемых существ
   !!co&y8=-1:; Пропускаем существо, если оно ценное
   !!VRy7&y8<>0:Sy8; Изменяем, если нужно
   !!HE-1:C0/i/?y4/d-y7; Забираем их из армии
   !!VRy7:-1; Снижаем значение переменной на 1 для корректной работы цикла
   !!re j/0/y7:;
    !!MNv998/v999/v1000:Mj/y4/1; Ставим охранников в слоты шахты
   !!en:;

   !!br:; Прерываем цикл, если расставили охрану
  !!en:;
 !!en:;
!!en:;

* Настройка количества существ, которые могут быть оставлены.
!?PI;
!!SN:W^StrongLvl1Cr246^/6; Волшебница
!!SN:W^StrongLvl1Cr14^/5; Кентавр
!!SN:W^StrongLvl1Cr15^/5; Капитан Кентавров
!!SN:W^StrongLvl1Cr192^/5; Сильванский кентавр
!!SN:W^StrongLvl1Cr173^/2; Санта-гремлин
!!SN:W^StrongLvl1Cr0^/6; Копьеносец
!!SN:W^StrongLvl1Cr1^/6; Алебардист
!!SN:W^StrongLvl1Cr29^/6; Мастер-гремлин
!!SN:W^StrongLvl1Cr239^/6; Оборотень
!!SN:W^StrongLvl1Cr213^/6; Суккуб
!!SN:W^StrongLvl1Cr225^/6; Троглодит-Изверг

!!SN:W^StrongLvl1Cr124^/-1; Паук-падальщик
!!SN:W^StrongLvl1Cr126^/-1; Оса-падальщик
; Насекомых не оставляем, так как они нужны для приручения Подземных пожирателей.


********************************************************************************
